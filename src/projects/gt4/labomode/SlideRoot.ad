module PROJECT::ROOT
{
	#include "../slide/musiccontrol.h"

	function onInitialize(context)
	{
		print("enter.");
		music_init(context);
		music_start();
		slide.doPlay();
		context.cursor_visible = false;
	}
	
	function onFinalize(context)
	{
		print("leave.");
		context.cursor_visible = true;
	}
	
	function exit(context)
	{
		main::sound.play("cancel");
		music_stop(context);
		slide.doStop();
		start_page(context, PhotoRoot);
	}

	function onCancel(context)
	{
		exit(context);
		return EVENTRESULT_FILTER;
	}
	
	function onKeyPress(context, event)
	{
		if (event.keysym == PS2_PAD_CTRL_START || event.keysym == PS2_PAD_CTRL_CROSS)
		{
			exit(context);
			return EVENTRESULT_FILTER;
		}
		return EVENTRESULT_CONTINUE;
	}
	
	function setShuffle(m)
	{
		slide.shuffle = m;
	}

	static music_list = nil;
	static music_map = nil;
	static watcher = nil;

	function music_init(context)
	{
		init_music_player();
		music_player_set_volume(main::game.option.slide_bgm_volume * 1.3);
		init_base_music_list();
		music_player_set_infinity_loop(true);
	
		music_list = Array();
		music_map = make_music_map();
	
		load_slide_playlist(music_list, music_map);
	
		watcher = main::menu::MScriptWatcher(context, music_tick);
		watcher.interval = 30;
		watcher.append();
	}

	function music_exit(context)
	{
		if (watcher != nil)
			watcher.remove();
	}

	function music_start()
	{
		var listsize = music_list.size;
	
		var entry_list = Array(listsize);
		var id_list = Array(listsize);
	
		for (var i = 0; i < listsize; i++)
		{
			entry_list[i] = i;
			id_list[i] = music_list[i][3];
		}
	
		var shuffle = get_shuffle_slide_playlist();
		music_player_entry_list(id_list, entry_list, shuffle);
		music_player_do_play();
	}

	function music_stop(context)
	{
		music_player_do_stop();
	}

	function music_tick(context)
	{
		var event = music_player_tick(context);
	
		switch(event)
		{
			case 0:
				break;
			case 3:
				break;
			case 1:
				var index = music_player_get_now_index();
				var id = music_player_get_now_id();
				var info = get_base_music_list_information(id);
			
				if (info.size)
					ROOT["information"].text = "%s/%s/%s".format(info[1], info[0], info[2]);
				break;

			case 2:
				break;
		}
	}
}