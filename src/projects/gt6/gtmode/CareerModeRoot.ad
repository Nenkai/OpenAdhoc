













module ROOT
{
    CommonTemplate.visible = false;

    static sCategory;
    static sNeedNewOpneSE = false;

    attribute gp;

    method open(context, category)
    {
        if (category == nil)
            category = PROJECT.history['CareerModeRoot::Category'];

        if (category == nil)
            return;

            PROJECT.history['LastPageName'] = ROOT.name;
        PROJECT.history['CareerModeRoot::Category'] = category;

        sCategory = category;
        SequenceUtil::enableStartMenuRoot();
        SequenceUtil::enableSceneFaceRoot();
        SequenceUtil::startPageSimple(context, self);
    }

    method close(context, goto_race)
    {
        GAME_STATUS.user_profile.context.career_mode_type = -1;

        var actor = self.CloseActor;
        actor.forward();
        actor.restart();
        context.wait(actor.Fade.total_time);

        sCategory = nil;

        if (!goto_race)
        {
            PROJECT.history['LastPageName'] = nil;
            PROJECT.history['CareerModeRoot::Category'] = nil;
            GTModeRoot.open(context);
        }
    }

    method onLoad(context)
    {
        if (!PROJECT.defined("CareerModeUtil"))
            PROJECT.load("/scripts/gt6/util/CareerModeUtil");
    }

    method sequence_loop(context)
    {
        var goto_race = false;
        var selected_folder = &PROJECT.history['CareerModeRoot::SelectFolder'];
        for (;;)
        {
            if (*selected_folder == nil)
            {
                var event_folder = EventFolderSelect.open(context);
                if (event_folder != nil)
                    *selected_folder = event_folder;
                else
                    break;
            }

            var gp = EventSelect.open(context, *selected_folder);
            if (gp == nil)
            {
                *selected_folder = nil;
                continue;
            }
    
            BG.hide();
    
            var goto_race = RaceEntryRoot.modal_open(context, gp, nil, sCategory);
            if (goto_race)
                break;
        }

        self.close(context, goto_race);
    }

    method onInitialize(context)
    {
        checkPlayEnding(context);

        main::Grim2.requestExtendSession(args => {});

        CursorProject::SceneFaceRoot.requestParticleCommand(context, "topMenuParticle_Shape02");
        
        Title.text = CareerModeUtil::GetCategoryText(sCategory);
        
        var category_str = CareerModeUtil::GetCategoryString(sCategory);
        CareerLogo.image_path = "/image/gt6/event/mark_%{category_str.downcase()}.dds";

        var mcolor = menu::MColorObject(0, 0, 0);

        if (self.ColorSet.defined(category_str))
            mcolor = self.ColorSet[category_str];

        BG::base.setColor(mcolor);
        EventFolderSelect::Template::ExtraEventItem::bg.setColor(mcolor);
        BG.appear(true);
        BG.stretchV(true);
        self.OpenActor.forward();
        self.OpenActor.restart();
        self.CloseActor.stop();

        PROJECT.PlayMenuBGM(context, ROOT);
        context.pushEvent(main::menu::MFunctionEvent(sequence_loop, context));
    }

    method onFinalize(context)
    {
        main::Grim2.clearCallback();
    }

    method checkPlayEnding(context)
    {
        if (GAME_STATUS.user_profile.record.event_record.isFolderCleared(42))
        {
            if (!KeyValueUtil::getFlag_NotNil("career", "play_ending"))
            {
                KeyValueUtil::set("career", "play_ending", true);

                CursorProject::SceneFaceRoot.visible = false;
                PageUtil::invisibleUnderPages(context, self);

                EndingMovieRoot.openModal(context, EndingMovieRoot::OpenMode::CAREER_GRAND_END);

                CursorProject::SceneFaceRoot.visible = true;
                PageUtil::visibleUnderPages(context, self);
            }

            if (!KeyValueUtil::getFlag_NotNil("career", "get_ending_music"))
            {
                unlockBonusMusicAfterEnding(context);
                KeyValueUtil::set("career", "get_ending_music", true);
            }
        }
        else if (GAME_STATUS.user_profile.record.event_record.isFolderCleared(60) && !KeyValueUtil::getFlag_NotNil("career", "play_short_ending"))
        {
            KeyValueUtil::set("career", "play_short_ending", true);

            CursorProject::SceneFaceRoot.visible = false;
            PageUtil::invisibleUnderPages(context, self);

            EndingMovieRoot.openModal(context, EndingMovieRoot::OpenMode::CAREER_SHORT_END);

            CursorProject::SceneFaceRoot.visible = true;
            PageUtil::visibleUnderPages(context, self);
        }
    }

    function unlockBonusMusicAfterEnding(context)
    {
        var data_list = MusicFileUtil::getBgmListByUnlockType(1, 1);
        if (data_list.size == 0)
            return;

        var id_list = Array();
        var music_titles = "";

        foreach (var i in data_list)
        {
            id_list.push(i.id);
            music_titles += music_titles != "" ? "\n" : ("" + "%s / %s".format(i.title, i.artist));
        }

        MusicFileUtil::setHiddenByList(id_list, false);
        var key = "Message_MusicAdded";

        var fmt = manager.translate("GTModeProject::GTModeRoot", key);
        if (fmt == key)
            return;

        SoundUtil::PlayStreamSE("sound_gt/se/gt6unlock");
        DialogUtil::openConfirmDialog(context, DialogUtil::OK, fmt.build(music_titles));
    }

    method openLicenseGetRoot(context, idx)
    {
        PageUtil::invisibleUnderPages(context, self);
        LicenseGetRoot.open(context, idx);
        PageUtil::visibleUnderPages(context, self);
    }

    method showTutorial(context)
    {
        var data = 
        [
            // Go to B
            [
                "check_begin" : "complete_career_n",
                "done" : "complete_career_n",
                "begin" : "goto_b",
                "exec_func" : (context, args) => 
                {
                    openLicenseGetRoot(context, 0);
                }
            ],
            // Go to A
            [
                "check_begin" : "complete_career_b",
                "done" : "complete_career_b",
                "begin" : "goto_a",
                "exec_func" : (context, args) => 
                {
                    openLicenseGetRoot(context, 1);
                }
            ],
            // Go to IB
            [
                "check_begin" : "complete_career_a",
                "done" : "complete_career_a",
                "begin" : "goto_ib",
                "exec_func" : (context, args) => 
                {
                    openLicenseGetRoot(context, 2);
                }
            ],
            // Go to IA
            [
                "check_begin" : "complete_career_ib",
                "done" : "complete_career_ib",
                "begin" : "goto_ia",
                "exec_func" : (context, args) => 
                {
                    openLicenseGetRoot(context, 3);
                }
            ],
            // Go to S
            [
                "check_begin" : "complete_career_ia",
                "done" : "complete_career_ia",
                "begin" : "goto_s",
                "exec_func" : (context, args) => 
                {
                    openLicenseGetRoot(context, 4);
                }
            ],
            // Career Finished
            [
                "check_begin" : "complete_career_s",
                "done" : "complete_career_s",
                "message" : "complete_career_s",
                "msg_param" : [
                    "open_wait_sec" : 1.0,
                ],
            ],
            // Open Sunday Cup
            [
                "check_begin" : "open_enjoycup",
                "done" : "open_enjoycup",
                "message" : "open_enjoycup",
                "msg_param" : [
                    "open_wait_sec" : 1.0,
                ],
            ],
            // Open Coffee Break
            [
                "check_begin" : "open_coffee_break",
                "done" : "open_coffee_break",
                "message" : "open_coffee_break",
                "msg_param" : [
                    "open_wait_sec" : 1.0,
                ],
            ],
            // Open Missions
            [
                "check_begin" : "open_mission_race",
                "done" : "open_mission_race",
                "message" : "open_mission_race",
                "msg_param" : [
                    "open_wait_sec" : 1.0,
                ],
            ],
            // Open One Makes
            [
                "check_begin" : "open_onemake_race",
                "done" : "open_onemake_race",
                "message" : "open_onemake_race",
                "msg_param" : [
                    "open_wait_sec" : 1.0,
                ],
            ],
            // Open B License
            [
                "check_begin" : "open_b_license",
                "done" : "open_b_license",
                "message" : "open_b_license",
                "msg_param" : [
                    "open_wait_sec" : 1.0,
                ],
            ],
            // Start Menu
            [
                "check_begin" : "start_menu",
                "done" : "start_menu",
                "message" : "start_menu",
                "msg_param" : [
                    "open_wait_sec" : 1.0,
                ],
            ],
        ];

        TutorialUtil::showTutorial(context, data, "gttop");
    }

    method checkEventOpenMessage(context, folderID)
    {
        var id_list = [
            24,
            126,
            98,
            131,
            93,
        ];

        var complete_key_list = [
            24 : "open_enjoycup",
            126 : "open_coffee_break",
            98 : "open_mission_race",
            131 : "open_onemake_race",
            93 : "open_b_license",
        ];

        foreach (var id in id_list)
        {
            if (id == folderID)
                TutorialUtil::begin("gttop", complete_key_list[id]);
        }

        sNeedNewOpneSE = true;
        KeyValueUtil::set("open_event", folderID, true);
    }

    method checkClearLicenseAndMission(context, event_folder, folder_info)
    {
        var category_license_list = [
            "license_b",
            "license_a",
            "license_ib",
            "license_ia",
            "license_s",
        ];

        var complete_key_list = [
            "license_b" : "complete_career_n",
            "license_a": "complete_career_b",
            "license_ib": "complete_career_a",
            "license_ia": "complete_career_ib",
            "license_s": "complete_career_ia",
        ];

        var trophy_id_list = [
            "license_b" : gtengine::TrophyType::LICENSE_GOLD_B,
            "license_a": gtengine::TrophyType::LICENSE_GOLD_A,
            "license_ib": gtengine::TrophyType::LICENSE_GOLD_IB,
            "license_ia": gtengine::TrophyType::LICENSE_GOLD_IA,
            "license_s": gtengine::TrophyType::LICENSE_GOLD_S,
        ];

        foreach (var license_folder_name in category_license_list)
        {
            if (event_folder == license_folder_name)
            {
                if (GAME_STATUS.user_profile.record.event_record.isFolderCleared(folder_info["id"]))
                {
                    if (GAME_STATUS.user_profile.record.event_record.isFolderCleared(folder_info["id"], 0))
                    {
                        var trophyId = trophy_id_list[license_folder_name];
                        if (trophyId != nil)
                            PDINetwork.unlockTrophyAsync(trophyId);
                    }

                    TutorialUtil::begin("gttop", complete_key_list[license_folder_name]);
                }
            }
        }

        showTutorial(context);
        checkPresentCarsAtLicenseAndMission(context, event_folder, folder_info["id"]);
    }

    method checkPresentCarsAtLicenseAndMission(context, event_folder, folder_id)
    {
        var present_array = [
            [ event_folder == "license_b" && GAME_STATUS.user_profile.record.event_record.isFolderCleared(folder_id, 2), "present_license_b_bro", "PRESENT_LICENSE_0_B_BRONZE", "pdi_racing_kart_50_xx"],
            [ event_folder == "license_b" && GAME_STATUS.user_profile.record.event_record.isFolderCleared(folder_id, 0), "present_license_b", "PRESENT_LICENSE_0_B", "mito_14_t_sport_09"],
            [ event_folder == "license_a" && GAME_STATUS.user_profile.record.event_record.isFolderCleared(folder_id, 2), "present_license_a_bro", "PRESENT_LICENSE_1_A_BRONZE", "s2000_06"],
            [ event_folder == "license_a" && GAME_STATUS.user_profile.record.event_record.isFolderCleared(folder_id, 0), "present_license_a", "PRESENT_LICENSE_1_A", "x_bow_street_12"],
            [ event_folder == "license_ib" && GAME_STATUS.user_profile.record.event_record.isFolderCleared(folder_id, 2), "present_license_ib_bro", "PRESENT_LICENSE_2_IB_BRONZE", "scirocco_r_10"],
            [ event_folder == "license_ib" && GAME_STATUS.user_profile.record.event_record.isFolderCleared(folder_id, 0), "present_license_ib", "PRESENT_LICENSE_2_IB", "sl55_amg_02"],
            [ event_folder == "license_ia" && GAME_STATUS.user_profile.record.event_record.isFolderCleared(folder_id, 2), "present_license_ia_bro", "PRESENT_LICENSE_3_IA_BRONZE", "evoque_coupe_dynamic_13"],
            [ event_folder == "license_ia" && GAME_STATUS.user_profile.record.event_record.isFolderCleared(folder_id, 0), "present_license_ia", "PRESENT_LICENSE_3_IA", "hep_g37_08"],
            [ event_folder == "license_s" && GAME_STATUS.user_profile.record.event_record.isFolderCleared(folder_id, 2), "present_license_s_bro", "PRESENT_LICENSE_4_S_BRONZE", "tesla_roadster_08"],
            [ event_folder == "license_s" && GAME_STATUS.user_profile.record.event_record.isFolderCleared(folder_id, 0), "present_license_s", "PRESENT_LICENSE_4_S", "r8_lms_ultra_12"],
            [ event_folder == "mission_b" && GAME_STATUS.user_profile.record.event_record.isFolderCleared(folder_id, 0), "present_mission_b", "PRESENT_MISSION_0_B", "lancer_evo6_tommi_gsr_scp_00"],
            [ event_folder == "mission_a" && GAME_STATUS.user_profile.record.event_record.isFolderCleared(folder_id, 0), "present_mission_a", "PRESENT_MISSION_1_A", "concept_1_series_tii_07"],
            [ event_folder == "mission_ib" && GAME_STATUS.user_profile.record.event_record.isFolderCleared(folder_id, 0), "present_mission_ib", "PRESENT_MISSION_2_IB", "elise_96_rm"],
            [ event_folder == "mission_ia" && GAME_STATUS.user_profile.record.event_record.isFolderCleared(folder_id, 0), "present_mission_ia", "PRESENT_MISSION_3_IA", "_430_scuderia_07"],
            [ event_folder == "mission_s" && GAME_STATUS.user_profile.record.event_record.isFolderCleared(folder_id, 0), "present_mission_s", "PRESENT_MISSION_4_S", "gtr_n24_gta_12"],
        ];

        checkPresentCondition(context, event_folder, folder_id, nil, present_array);
    }

    function checkGoodWoodGold(context, gp, check_idx)
    {
        var good_wood_check_array = [
            [0, 3],
            [3, 6],
            [6, 9],
            [9, 12],
            [12, 15],
        ];

        |var start_idx, var end_idx| = good_wood_check_array[check_idx];
        var is_gold = true;

        for (var event_idx = start_idx; event_idx < end_idx; event_idx++)
        {
            var event = gp.events[event_idx];
            var result = RaceEventUtil::getResult(event.event_id);
            if (result != 0)
            {
                is_gold = false;
                break;
            }
        }

        return is_gold;
    }

    method checkPresentCarsAtGoodWood(context, event_folder, gp)
    {
        var present_array = [
            [ event_folder == "gw_fos" && checkGoodWoodGold(context, gp, 0), "present_gw_fos1_gold", "PRESENT_GOODWOOD_1_GOLD", "rocket_07"],
            [ event_folder == "gw_fos" && checkGoodWoodGold(context, gp, 1), "present_gw_fos2_gold", "PRESENT_GOODWOOD_2_GOLD", "xnr_ghia_roadster_60"],
            [ event_folder == "gw_fos" && checkGoodWoodGold(context, gp, 2), "present_gw_fos3_gold", "PRESENT_GOODWOOD_3_GOLD", "corvette_racer_concept_59"],
            [ event_folder == "gw_fos" && checkGoodWoodGold(context, gp, 3), "present_gw_fos4_gold", "PRESENT_GOODWOOD_4_GOLD", "cizeta_v16t_94_prm"],
            [ event_folder == "gw_fos" && checkGoodWoodGold(context, gp, 4), "present_gw_fos5_gold", "PRESENT_GOODWOOD_5_GOLD", "gt_by_citroen_road_xx"],
        ];

        checkPresentCondition(context, event_folder, nil, gp, present_array);
    }

    method checkPresentCondition(context, event_folder, folder_id, gp, present_array)
    {
        foreach (|var condition, var key, var translate, var carcode| in present_array)
        {
            if (condition)
            {
                if (!KeyValueUtil::getFlag_NotNil("career", key))
                {
                    if (main::GAME_STATUS.user_profile.garage.isFull())
                    {
                        sound.play("disable");
                        DialogUtil::openConfirmDialog(context, DialogUtil::ERROR,
                            manager.translate(CarShowroomRoot, "Message_GarageFullAtPresentCar"));
                        return;
                    }

                    var cp = gtengine::MCarParameter(carcode);
                    cp.setNewCar();

                    CursorProject::SceneFaceRoot.visible = false;

                    var message = manager.translate("CommonGTMode", translate);
                    KeyValueUtil::set("career", key, true);
                    CarDeliveryRoot.openModal(context, CarDeliveryRoot::OPEN_MODE::PRESENT, cp, message);

                    CursorProject::SceneFaceRoot.visible = true;
                }
            }
        }
    }

    method checkPresentCarsByStar(context, category_str, current, total)
    {
        var present_array = [
            [ category_str == "N" && current >= total, "present_star_n_comp", "PRESENT_STAR_00_N_COMP", "clio_renaultsport_11"],
            [ category_str == "B" && current >= total / 2, "present_star_b_half", "PRESENT_STAR_01_B_HALF", "delta_hf_integrale_evo_91"],
            [ category_str == "B" && current >= total, "present_star_b_comp", "PRESENT_STAR_02_B_COMP", "acura_nsx_91"],
            [ category_str == "A" && current >= total / 2, "present_star_a_half", "PRESENT_STAR_03_A_HALF", "challenger_rt_70"],
            [ category_str == "A" && current >= total, "present_star_a_comp", "PRESENT_STAR_04_A_COMP", "amemiya_fd3s_rx7_xx"],
            [ category_str == "IB" && current >= total / 2, "present_star_ib_half", "PRESENT_STAR_05_IB_HALF", "xkr_s_11"],
            [ category_str == "IB" && current >= total, "present_star_ib_comp", "PRESENT_STAR_06_IB_COMP", "diablo_gt2_98"],
            [ category_str == "IA" && current >= total / 2, "present_star_ia_half", "PRESENT_STAR_07_IA_HALF", "nascar_chevrolet_ss_24_13"],
            [ category_str == "IA" && current >= total, "present_star_ia_comp", "PRESENT_STAR_08_IA_COMP", "enzo_ferrari_02"],
            [ category_str == "S" && current >= total / 2, "present_star_s_half", "PRESENT_STAR_09_S_HALF", "mclaren_f1_94"],
            [ category_str == "S" && current >= total, "present_star_s_comp", "PRESENT_STAR_10_S_COMP", "quattro_s1_pikespeak_87"],
        ];

        foreach (|var condition, var key, var translate_key, var carcode| in present_array)
        {
            if (condition)
            {
                if (!KeyValueUtil::getFlag_NotNil("career", key))
                {
                    if (main::GAME_STATUS.user_profile.garage.isFull())
                    {
                        sound.play("disable");
                        DialogUtil::openConfirmDialog(context, DialogUtil::ERROR,
                            manager.translate(CarShowroomRoot, "Message_GarageFullAtPresentCar"));
                        return;
                    }

                    var cp = gtengine::MCarParameter(carcode);
                    cp.setNewCar();

                    CursorProject::SceneFaceRoot.visible = false;

                    var message = manager.translate("CommonGTMode", translate_key);
                    KeyValueUtil::set("career", key, true);
                    CarDeliveryRoot.openModal(context, CarDeliveryRoot::OPEN_MODE::PRESENT, cp, message);

                    CursorProject::SceneFaceRoot.visible = true;
                }
            }
        }
    }

    method DisableEventMessage(context, folder_info)
    {
        if (folder_info.NeedFolderID > 0)
        {
            var lang = main::pdistd::MLocale::getLanguage();
            var folder_event_text = MENUDB.executeQuery1R1C("SELECT L.%{lang} FROM t_event_folder F, t_event_folder_localize L WHERE F.TitleID = L.LocalizeID AND F.FolderID=?",
                                    [folder_info.NeedFolderID]);
            ERROR(context, "CommonGTMode", "Message_NeedFolderID", folder_event_text);
        }
    }

    module BG
    {
        method onInitialize(context)
        {
            var target_src = EventFolderSelect::EventFolderList;
            var target_dst = EventSelect::EventList;

            self.Transform::XY.setStartValue(0, [target_src.x, target_src.y]);
            self.Transform::WH.setStartValue(0, [target_src.w, target_src.h]);

            self.Transform::XY.setEndValue(0, [target_dst.x, target_dst.y]);
            self.Transform::WH.setEndValue(0, [target_dst.w, target_dst.h]);
        }

        method appear(warp)
        {
            self.Fade.backward();
            self.Fade.start();
            if (warp == true)
            {
                self.Fade.warp();
            }
        }

        method hide(warp)
        {
            self.Fade.forward();
            self.Fade.start();
            if (warp == true)
            {
                self.Fade.warp();
            }
        }

        method stretchH(warp)
        {
            self.Transform.forward();
            self.Transform.start();
            self.gradationUD::Fade.forward();
            self.gradationUD::Fade.start();
            self.gradationLR::Fade.forward();
            self.gradationLR::Fade.start();

            if (warp == true)
            {
                self.Transform.warp();
                self.gradationUD::Fade.warp();
                self.gradationLR::Fade.warp();
            }
        }

        method stretchV(warp)
        {
            self.Transform.backward();
            self.Transform.start();
            self.gradationUD::Fade.backward();
            self.gradationUD::Fade.start();
            self.gradationLR::Fade.backward();
            self.gradationLR::Fade.start();

            if (warp == true)
            {
                self.Transform.warp();
                self.gradationUD::Fade.warp();
                self.gradationLR::Fade.warp();
            }
        }
    }

    module EventFolderSelect
    {
        Template.visible = false;

        attribute current_star;
        attribute result;
        attribute event_loop;

        method open(context)
        {
            ROOT.setFocus(nil);
            
            var success = self.initialize(context);
            if (!success)
                return nil;

            self.result= nil;
            self.Fade.forward();
            self.Fade.start();
            self.insensitive = false;

            self.event_loop = main::menu::EventLoop(context);
            self.event_loop.enter();
            
            self.insensitive = true;
            self.finalize(context);

            var res = self.result;
            self.result = nil;
            return res;
        }

        method close(context)
        {
            if (self.event_loop != nil)
            {
                self.Fade.backward();
                self.Fade.start();
                context.wait(self.Fade.ramain_time);
                self.event_loop.leave();
                self.event_loop = nil;
            }

            ROOT.setFocus(nil);
        }

        method initialize(context)
        {
            BG.stretchV();
            BG.appear();

            var category_str = CareerModeUtil::GetCategoryString(sCategory);
            var main_event_type = CareerModeUtil::GetEventType(sCategory, CareerModeUtil::TYPE::MAIN_EVENT);
            var current_stars = GAME_STATUS.user_profile.record.event_record.getStars(main_event_type, nil);
            var total_stars = MENUDB.executeQuery1R1C("SELECT SUM(Star) FROM t_event_folder WHERE Type=?", [main_event_type]);
            var category_key_name = "prev_star_at_category{0}".build(sCategory);
            var prev_stars = KeyValueUtil::getInt("career", category_key_name);

            TotalStar::HBox::Current.text = current_stars;
            TotalStar::HBox::Total.text = "/%d".format(total_stars);
            checkPresentCarsByStar(context, category_str, current_stars, total_stars);

            var success = EventFolderList.initialize(context, sCategory, current_stars, prev_stars);
            if (!success)
                return false;

            ExtraEvents.initialize(context, sCategory, current_stars, prev_stars);

            if (ROOT.getFocusWidget() == nil)
                ROOT.setFocus(EventFolderList::ListBox);

            context.pushEvent(menu::MScriptEvent(context, ROOT, "showTutorial"));

            if (sNeedNewOpneSE)
            {
                SoundUtil::PlayStreamSE("sound_gt/se/gt6unlock");
                sNeedNewOpneSE = false;
            }

            KeyValueUtil::begin();
            KeyValueUtil::set("career", category_key_name, current_stars);
            KeyValueUtil::commit();

            return true;
        }

        method finalize(context)
        {
            EventFolderList.finalize(context);
        }

        method onInitialize(context)
        {
            self.Fade.forward();
            self.Fade.reset();
            self.insensitive = true;
        }

        method onCancel(context)
        {
            sound.play("cancel");
            self.close(context);
            return EVENTRESULT_FILTER;
        }

        method onKeyPress(context, event)
        {
            if (CursorProject::StartMenuRoot::StartMenu.checkKeyPress(context, event))
                return EVENTRESULT_FILTER;

            switch (event.keysym)
            {
                case CELL_PAD_CTRL_SELECT:
                    if (PROJECT::IsDevelop(context))
                    {
                        var state = context.getUpdateContext().getPadButtonState(0);
                        if (state & 0x800)
                        {
                            CursorProject::SceneFaceRoot.visible = false;
                            PageUtil::invisibleUnderPages(context, self);
                            EndingMovieRoot.openModal(context, true);
                            CursorProject::SceneFaceRoot.visible = true;
                            PageUtil::visibleUnderPages(context, self);
                        }
                        else if (state & 0x100)
                        {
                            CursorProject::SceneFaceRoot.visible = false;
                            PageUtil::invisibleUnderPages(context, self);
                            EndingMovieRoot.openModal(context, false);
                            CursorProject::SceneFaceRoot.visible = true;
                            PageUtil::visibleUnderPages(context, self);

                        }
                    }
                    else
                        ;
            }

            return EVENTRESULT_CONTINUE;
        }

        module EventFolderList
        {
            method initialize(context, category, current_stars, prev_stars)
            {
                var lang = main::pdistd::MLocale::getLanguage();
                var main_event_type = CareerModeUtil::GetEventType(category, CareerModeUtil::TYPE::MAIN_EVENT);
                var info_list = MENUDB.queryList("SELECT F.*, L.%{lang} FROM t_event_folder F, t_event_folder_localize L WHERE F.TitleID = LocalizeID AND F.Type=? ORDER BY F.FolderOrder",
                                                [main_event_type]);
                
                if (info_list.size == 0)
                    return false;

                var license_event_type = CareerModeUtil::GetEventType(category, CareerModeUtil::TYPE::LICENSE);
                var license_info = MENUDB.query1("SELECT F.*, L.%{lang} FROM t_event_folder F, t_event_folder_localize L WHERE F.TitleID = LocalizeID AND F.Type=? ORDER BY F.FolderOrder",
                                                [license_event_type]);

                Target.visible = false;
                if (license_info != nil)
                {
                    if (license_info.NeedFolderID > 0 && 
                        !GAME_STATUS.user_profile.record.event_record.isFolderCleared(license_info.NeedFolderID))
                    {
                        var folder_info;
                        for (var i = 0; i < info_list.size; i++)
                        {
                            folder_info = info_list[i];
                            if (license_info.NeedFolderID == folder_info.FolderID)
                            {
                                info_list.erase(i);
                                break;
                            }

                            folder_info = nil;
                        }

                        if (folder_info != nil)
                        {
                            Target::License.setInfo(context, folder_info, current_stars, prev_stars, false);
                            Target.visible = true;
                        }
                    }
                    else
                    {
                        Target::License.setInfo(context, license_info, current_stars, prev_stars, true);
                        Target.visible = true;
                    }
                }

                self.ListBox.setItemTemplate(context, Template::EventFolderItem);
                self.ListBox.setItemCount(info_list.size);

                var focus_index = 0;
                for (var i = 0; i < info_list.size; i++)
                {
                    var info = info_list[i];
                    var w = self.ListBox.getItemWidget(i, context);
                    w.setInfo(context, info, current_stars, prev_stars);

                    if (PROJECT.history['CareerModeRoot::FocusFolder'] == info.Name)
                    {
                        PROJECT.history['CareerModeRoot::FocusFolder'] = nil;
                        focus_index = i;
                        self.ListBox.save_index = focus_index;
                        ROOT.setFocus(self.ListBox);
                    }
                }

                var adj = main::menu::MAdjustment();
                self.ListBox.v_adjustment = adj;
                self.VScrollArrow.adjustment = adj;
                return true;
            }

            method finalize(context)
            {
                self.ListBox.setItemCount(0);
                self.ListBox.focus_index = 0;
            }

            module ListBox
            {
                method warpFocus(context, focus_index)
                {
                    var ratio = self.interpolate_ratio;
                    self.interpolate_ratio = 1.0;
                    
                    self.focus_index = focus_index;
                    context.wait();
                    self.interpolate_ratio = ratio;
                }

                method onActivate(context)
                {
                    var w = self.getItemWidget(self.focus_index, context);
                    if (w.defined("on_activate"))
                       return  w.on_activate(context);
                    
                    return EVENTRESULT_CONTINUE;
                }

                method onFocusEnter(context)
                {
                    var w = self.getItemWidget(self.focus_index, context);
                    if (w != nil && w.defined("focus_enter"))
                    {
                        w["focus_enter"](context);
                        ROOT.setFocus(w);
                    }

                    return EVENTRESULT_STOP;
                }

                method onFocusLeave(context)
                {
                    var w = self.getItemWidget(self.focus_index, context);
                    if (w != nil && w.defined("focus_leave"))
                    {
                        w["focus_leave"](context);
                    }

                    return EVENTRESULT_STOP;
                }

                method onFocusNotify(context, new_index, old_index)
                {
                    if (new_index != nil)
                    {
                        var w = self.getItemWidget(new_index, context);
                        if (w != nil && w.defined("focus_enter"))
                        {
                            w["focus_enter"](context);
                            ROOT.setFocus(w);
                        }
                    }

                    if (old_index != nil)
                    {
                        var w = self.getItemWidget(old_index, context);
                        if (w != nil && w.defined("focus_leave"))
                        {
                            w["focus_leave"](context);
                        }
                    }

                    return EVENTRESULT_STOP;
                }
            }

            module Target::License
            {
                attribute info;
                attribute is_license_folder;

                method onActivate(context)
                {
                    if (!self.active2)
                    {
                        ROOT::DisableEventMessage(context, info);
                        sound.play("disable");
                        return EVENTRESULT_FILTER;
                    }

                    sound.play("ok");
                    PROJECT.history['CareerModeRoot::FocusFolder'] = self.info.Name;

                    KeyValueUtil::set("open_event", self.info.FolderID, false);
                    if (is_license_folder)
                        RaceEventRoot.open(context, ROOT::sCategory, self.info.Name);
                    else
                    {
                        EventFolderSelect.result = self.info.Name;
                        EventFolderSelect.close(context);
                    }
                    return EVENTRESULT_FILTER;
                }

                method setInfo(context, info, current_stars, prev_stars, is_license_folder)
                {
                    self.info = info;
                    self.is_license_folder = is_license_folder;

                    var lang = main::pdistd::MLocale::getLanguage();
                    var event_name = info[lang];

                    self.Label.text = event_name;
                    self.Label_i.text = event_name;
                    self.Open.visible = false;

                    if (is_license_folder)
                    {
                        self.Flyer.visible = false;
                        self.CareerMark.visible = true;
                        self.panel_license.visible = true;
                        self.panel_color.visible = false;

                        var next_category = CareerModeUtil::GetNextCategory(ROOT::sCategory);
                        var next_category_str = CareerModeUtil::GetCategoryString(next_category);

                        self.CareerMark::CareerColor.setColor(ROOT.ColorSet[next_category_str]);
                        self.CareerMark::CareerLogo.image_path = "/image/gt6/event/mark_%{next_category_str.downcase()}.dds";

                        var category_text = CareerModeUtil::GetCategoryText(ROOT::sCategory);
                        Target::TextFace.text = manager.translate("CommonGTMode", "Label_CategoryLastEvent").build(category_text);
                    }
                    else
                    {
                        self.CareerMark.visible = false;
                        self.Flyer.visible = true;

                        var category_str = CareerModeUtil::GetCategoryString(sCategory);
                        self.panel_license.visible = false;
                        self.panel_color.setColor(self.ColorSet[category_str]);
                        self.panel_color.visible = true;
                        
                        var image_face = self.Flyer::ImageFace;
                        var end_load_img = method (context)
                        {
                            image_face.FadeActor.start();
                            image_face.on_delay_load_complete = nil;
                        };

                        image_face.on_delay_load_complete = *self.*end_load_img;
                        image_face.image_path = "piece/gt6/event_flyer/%s.img".format(info.Name);

                        var category_text = CareerModeUtil::GetCategoryText(ROOT::sCategory);
                        Target::TextFace.text = manager.translate("CommonGTMode", "Label_MainEventFinal").build(category_text);
                    }

                    if (current_stars < info.NeedStar)
                    {
                        self.NeedStars::HBox::Label.text = info.NeedStar;
                        self.active2 = false;
                    }
                    else
                    {
                        if (prev_stars < info.NeedStar)
                        {
                            self.active2 = true;
                            checkEventOpenMessage(context, info.FolderID);
                        }
                        else
                        {
                            self.active2 = true;
                        }

                        self.Open.visible =  KeyValueUtil::getFlag_NotNil("open_event", info.FolderID);
                    }
                    
                    if (PROJECT.history['CareerModeRoot::FocusFolder'] == info.Name)
                    {
                        PROJECT.history['CareerModeRoot::FocusFolder'] = nil;
                        ROOT.setFocus(self);
                    }
                }
            }
        }

        module ExtraEvents
        {
            method initialize(context, category, current_starts, prev_stars)
            {
                var type_list = [
                    CareerModeUtil::TYPE::MISSION,
                    CareerModeUtil::TYPE::COFFEE_BREAK,
                    CareerModeUtil::TYPE::ONE_MAKE,
                    CareerModeUtil::TYPE::ENDURANCE,
                ];

                var image_list = [
                    CareerModeUtil::TYPE::MISSION : "mission",
                    CareerModeUtil::TYPE::COFFEE_BREAK : "coffee",
                    CareerModeUtil::TYPE::ONE_MAKE : "onemake",
                    CareerModeUtil::TYPE::ENDURANCE : "endurance",
                ];

                var lang = main::pdistd::MLocale::getLanguage();
                self.MBox.clearChildren(context);

                foreach (var type in type_list)
                {
                    var event_type = CareerModeUtil::GetEventType(sCategory, type);
                    var info = MENUDB.query1("SELECT F.*, L.%{lang} FROM t_event_folder F, t_event_folder_localize L WHERE F.TitleID = LocalizeID AND F.Type=? ORDER BY F.FolderOrder",
                                            [event_type]);
                    if (info != nil)
                    {
                        var w = Template::ExtraEventItem.doCopy();
                        w.setInfo(context, info, current_starts, image_list[type], prev_stars);
                        self.MBox.appendChild(context, w);

                        if (PROJECT.history['CareerModeRoot::FocusFolder'] == info.Name)
                        {
                            PROJECT.history['CareerModeRoot::FocusFolder'] = nil;
                            ROOT.setFocus(w);
                        }
                    }
                }
            }
        }

        module Template
        {
            module EventFolderItem
            {
                attribute info;

                method on_activate(context)
                {
                    if (!self.active2)
                    {
                        ROOT::DisableEventMessage(context, info);
                        sound.play("disable");
                        return EVENTRESULT_FILTER;
                    }

                    sound.play("ok");
                    KeyValueUtil::set("open_event", self.info.FolderID, false);
                    PROJECT.history['CareerModeRoot::FocusFolder'] = self.info.Name;
                    EventFolderSelect.result = self.info.Name;
                    EventFolderSelect.close(context);

                    return EVENTRESULT_FILTER;
                }

                method setCurrentStars(context, current, total)
                {
                    var mbox = self.CurrentStars::HBox;
                    mbox.clearChildren(context);

                    for (var i = 0; i < total; i++)
                    {
                        var w = CommonTemplate::Star.doCopy();
                        w.active = i < current;
                        mbox.appendChild(context, w);
                    }
                }

                method setInfo(context, info, current_stars, prev_stars)
                {
                    self.info = info;

                    var lang = main::pdistd::MLocale::getLanguage();
                    var event_name = info[lang];
                    
                    self.Name::Label.text = event_name;
                    self.Name::Label_i.text = event_name;
                    
                    var image_face = self.image.flyer;

                    var end_load_img = method (context)
                    {
                        image_face.FadeActor.start();
                        image_face.on_delay_load_complete = nil;
                    };

                    image_face.on_delay_load_complete = *self.*end_load_img;
                    image_face.image_path = "piece/gt6/event_flyer/%s.img".format(info.Name);
                    if (info.NeedFolderID > 0 && !GAME_STATUS.user_profile.record.event_record.isFolderCleared(info.NeedFolderID))
                    {
                        self.NeedStars.visible = false;
                        self.active2 = false;
                    }
                    else if (current_stars < info.NeedStar)
                    {
                        self.NeedStars::HBox::Label.text = "%d".format(info.NeedStar);
                        self.active2 = false;
                    }
                    else
                    {
                        var current_folder_stars = GAME_STATUS.user_profile.record.event_record.getStars(nil, info.FolderID);
                        self.setCurrentStars(context, current_folder_stars, info.Star);
                        self.active2 = true;

                        if (prev_stars < info.NeedStar)
                            checkEventOpenMessage(context, info.FolderID);
                        
                        self.Open.visible = KeyValueUtil::getFlag_NotNil("open_event", info.FolderID);
                    }
                }

                method focus_enter(context)
                {
                    self.blink::Fade.restart();
                    self.blink.visible = true;
                }

                method focus_leave(context)
                {
                    self.blink::Fade.reset();
                }
            }

            module ExtraEventItem
            {
                attribute info;

                method onActivate(context)
                {
                    if (!self.active2)
                    {
                        ROOT::DisableEventMessage(context, info);
                        sound.play("disable");
                        return EVENTRESULT_FILTER;
                    }

                    sound.play("ok");
                    KeyValueUtil::set("open_event", self.info.FolderID, false);
                    GAME_STATUS.user_profile.context.career_mode_type = CareerModeUtil::GetTypeFromEventType(self.info.Type);
                    PROJECT.history['CareerModeRoot::FocusFolder'] = self.info.Name;
                    RaceEventRoot.open(context, ROOT::sCategory, self.info.Name);

                    return EVENTRESULT_FILTER;
                }

                method setInfo(context, info, current_stars, image, prev_stars)
                {
                    self.info = info;

                    var lang = main::pdistd::MLocale::getLanguage();
                    var event_name = info[lang];
                    
                    self.ImageFace.image_path = "/image/gt6/event/%{image}.dds";
                    self.Label.text = event_name;
                    self.Label_i.text = event_name;

                    if (current_stars < info.NeedStar)
                    {
                        self.NeedStars::HBox::Label.text = "%d".format(info.NeedStar);
                        self.active2 = false;
                    }
                    else if (prev_stars < info.NeedStar)
                    {
                        self.active2 = true;
                        checkEventOpenMessage(context, info.FolderID);
                    }
                    else
                    {
                        self.active2 = true;
                        self.Open.visible = KeyValueUtil::getFlag_NotNil("open_event", info.FolderID);
                    }
                }
            }
        }
    }

    module EventSelect
    {
        Template.visible = false;

        attribute current_star;
        attribute result;
        attribute event_loop;
        attribute folder_info;
        attribute gp;
        attribute preserved_championship_flag;
        attribute can_take_spot_race;

        method open(context, event_folder)
        {
            ROOT.setFocus(nil);
            var success = self.initialize(context, event_folder);

            if (!success)
                return nil;

            self.result = nil;
            self.Fade.forward();
            self.Fade.start();

            self.insensitive = false;

            self.event_loop = main::menu::EventLoop(context);
            self.event_loop.enter();

            self.insensitive = true;

            var res = self.result;
            self.result = nil;
            self.finalize(context);
            return res;
        }

        method close(context)
        {
            if (self.event_loop != nil)
            {
                self.Fade.backward();
                self.Fade.start();
                context.wait(self.Fade.ramain_time);
                self.event_loop.leave();
                self.event_loop = nil;
            }
        }

        method initializeGameRecord(folder_info)
        {
            var event_id_list = folder_info["event_id_list"];
            var event_type = folder_info["event_type"];
            var folder_id = folder_info["id"];
            var category = 0;
            var is_online = false;
            
            if (folder_info["championship"])
            {
                var championship_event_id = folder_id;
                event_id_list.push(championship_event_id);
            }

            GAME_STATUS.user_profile.record.event_record.initializeRecord(event_id_list, event_type, folder_id, category, is_online);
        }

        method getFolderInfo(context, event_name)
        {
            var xml = "/game_parameter/gt6/event/%s.xml".format(event_name);
            var folder_info_list = EventRace2.decodeOfflineEventList(xml);

            if (folder_info_list.size == 0)
                return nil;

            GAME_STATUS.user_profile.context.event_name = event_name;
            return folder_info_list[0];
        }

        method getGP(context, folder_id)
        {
            var xml = "/game_parameter/gt6/event/r%03d.xml".format(folder_id);
            return GameParameterUtil::createFromFile(xml);
        }

        method updateRegulations(context)
        {
            self.gp.event_index = 0;
			
			var dummy_text;
            var passed_regulation = RaceEventUtil::checkRegulation(self.gp, &dummy_text); 

            var listbox = EventList::ListBox;
            
            for (var i = 0; i < listbox.getItemCount(); ++i)
            {
                var w = listbox.getInitializedWidget(context, i);
                if (w != nil)
                    w.updateRegulation(passed_regulation);
            }
        }

        method initialize(context, event_folder)
        {
            BG.stretchH();
            BG.appear();
            if (self.folder_info == nil || self.gp == nil)
            {
                var folder_info = self.getFolderInfo(context, event_folder);
                if (folder_info == nil)
                {
                    self.close(context);
                    return false;
                }

                var gp = self.getGP(context, folder_info["id"]);
                if (gp == nil)
                {
                    self.close(context);
                    return false;
                }

                self.folder_info = folder_info;
                self.gp = gp;
                self.preserved_championship_flag = gp.championship;
            }

            self.gp.championship = self.preserved_championship_flag;
            initializeGameRecord(folder_info);

            var event_size = gp.events.size;
            if (gp.championship)
            {
                ++event_size;
                var result = GAME_STATUS.user_profile.record.event_record.getEventResult(gp.folder_id);
                self.can_take_spot_race = result > -1;
            }

            var listbox = EventList::ListBox;
            listbox.setItemTemplate(context, Template::EventItem);
            listbox.setItemCount(event_size);

            listbox.spacing = gp.championship ? 100 : 60;
            listbox.focus_index = PROJECT.history['CareerModeRoot::FocusEvent'] != nil ? PROJECT.history['CareerModeRoot::FocusEvent'] : 0;
            
            PROJECT.history['CareerModeRoot::FocusEvent'] = nil;
            ROOT.setFocus(listbox);

            var adj = main::menu::MAdjustment();
            EventList::HScrollArrow.adjustment = adj;
            listbox.h_adjustment = adj;
            
            var image_face = self.flyer;

            var end_load_img = method (context)
            {
                image_face.FadeActor.start();
                image_face.on_delay_load_complete = nil;
            };

            image_face.on_delay_load_complete = *self.*end_load_img;
            image_face.image_path = "piece/gt6/event_flyer/%s.img".format(event_folder);

            EventFolderName::Label.text = RaceEventUtil::getFolderTitleText(folder_info);

            var scrollclip = Description::ScrolledWindow::clip;
            scrollclip.space::Text.text = RaceEventUtil::getFolderDescriptionText(folder_info);
            scrollclip.space.y = 0.0;
            scrollclip.manual_target_y = 0.0;

            ToolTip.tip.value = RaceEventUtil::getFolderCopyText(folder_info);

            context.pushEvent(menu::MScriptEvent(context, ROOT, "showTutorial"));
            updateRegulations(context);
            return true;
        }

        method finalize(context)
        {
            EventList::ListBox.setItemCount(0);
        }

        method onInitialize(context)
        {
            self.Fade.forward();
            self.Fade.reset();
            self.insensitive = true;

            EventFolderName::Label.text = "";
            EventFolderName::Course.text = "";

            var adj = main::menu::MAdjustment();
            Description::Scrollbar::VScrollbar.adjustment = adj;

            var scroll_clip = Description::ScrolledWindow::clip;
            scroll_clip.scroll_mode = 2;
            scroll_clip.v_adjustment = adj;
            ToolTip.tip.remove();
        }

        method onActivate(context)
        {
            var listbox = EventList::ListBox;
            var w = listbox.getItemWidget(listbox.focus_index);

            if (w == nil)
            {
                sound.play("disable");
                return EVENTRESULT_FILTER;
            }

            var gp = self.gp;
            if (gp.championship && !w.is_championship_item)
            {
                if (!self.can_take_spot_race)
                {
                    sound.play("disable");
                    ROOT.setFocus(listbox);
                    ERROR(context, "RaceEvent", "YOU_MUST_CLEAR_CHAMPIONSHIP");
                    return EVENTRESULT_FILTER;
                }
            }

            sound.play("ok");
            gp.event_index = w.event_index;
            gp.championship = w.is_championship_item;

            GAME_STATUS.user_profile.context.career_mode_type = CareerModeUtil::TYPE::MAIN_EVENT;
            PROJECT.history['CareerModeRoot::FocusEvent'] = listbox.focus_index;

            self.result = gp;
            self.close(context);
            return EVENTRESULT_FILTER;
        }

        method onCancel(context)
        {
            sound.play("cancel");
            self.folder_info = nil;
            self.gp = nil;
            self.preserved_championship_flag; // Normal
            self.close(context);

            return EVENTRESULT_FILTER;
        }

        method onKeyPress(context, event)
        {
            var preopen_garage_id = GAME_STATUS.user_profile.garage.getRidingGarageId();

            if (CursorProject::StartMenuRoot::StartMenu.checkKeyPress(context, event))
            {
                if (preopen_garage_id != GAME_STATUS.user_profile.garage.getRidingGarageId())
                    updateRegulations(context);
                return EVENTRESULT_FILTER;
            }

            return EVENTRESULT_CONTINUE;
        }

        module EventList::ListBox
        {
            method getInitializedWidget(context, i)
            {
                if (i < 0)
                    return nil;

                var w = self.getItemWidget(i);
                if (w == nil)
                {
                    w = self.getItemWidget(i, context);
                    var gp = EventSelect.gp;

                    if (gp.championship)
                    {
                        if (i == 0)
                        {
                            w.init(context, gp, 0, true);
                        }
                        else if (i - 1 < gp.events.size)
                        {
                            w.init(context, gp, i - 1, false, EventSelect.can_take_spot_race);
                        }
                        else
                        {
                            return nil;
                        }
                    }
                    else if (i < gp.events.size)
                    {
                        w.init(context, gp, i, false);
                    }
                    else
                    {
                        return nil;
                    }
                }

                return w;
            }

            method onFocusEnter(context)
            {
                var w = self.getInitializedWidget(context, self.focus_index);
                if (w != nil)
                {
                    if (w.defined("focus_enter"))
                        w["focus_enter"](context);
                    ROOT.setFocus(w);
                }

                return EVENTRESULT_STOP;
            }

            method onFocusNotify(context, new_index, old_index)
            {
                if (new_index != nil)
                {
                    var w = self.getInitializedWidget(context, new_index);
                    if (w != nil && w.defined("focus_enter"))
                    {
                        w["focus_enter"](context);
                    }
                }

                if (old_index != nil)
                {
                    var w = self.getInitializedWidget(context, old_index);
                    if (w != nil && w.defined("focus_leave"))
                    {
                        w["focus_leave"](context);
                    }
                }

                return EVENTRESULT_STOP;
            }

            method onVisibleNotify(context, indexes)
            {
                foreach (var i in indexes)
                {
                    self.getInitializedWidget(context, i);
                }

                return EVENTRESULT_STOP;
            }

            method onKeyPress(context, key_event)
            {
                var scrollclip = Description::ScrolledWindow::clip;
                var line_height = scrollclip.space.Text.line_height;

                switch (key_event.keysym)
                {
                    case CELL_PAD_CTRL_SELECT:
                        if (PROJECT::IsDevelop(context))
                        {
                            var state = context.getUpdateContext().getPadButtonState(0);
                            if (state & 0x800)
                            {
                                if (sCategory < CareerModeUtil::CATEGORY::S)
                                    openLicenseGetRoot(context, sCategory - 1);
                            }
                        }
                        else
                        {
                            var gp = EventSelect.gp;
                            var w = self.getItemWidget(self.focus_index);
                            if (w.is_championship_item)
                            {
                                var event_id = gp.folder_id;
                                var result = GAME_STATUS.user_profile.record.event_record.getEventResult(event_id);
                                if (--result < 0)
                                    result = gtengine::FinishResult::RANK_4;
                                
                                GAME_STATUS.user_profile.record.event_record.updateResultOnly(event_id, result);
                            }
                            else
                            {
                                var event = gp.events[w.event_index];
                                var event_id = event.event_id;
                                var star_max = event.reward.star_table.size;

                                // Typo is normal
                                var start_count = GAME_STATUS.user_profile.record.event_record.getStars(nil, nil, event_id);

                                if (start_count < star_max)
                                    GAME_STATUS.user_profile.record.event_record.updateStars(event_id, star_max);
                                else
                                {
                                    var result = GAME_STATUS.user_profile.record.event_record.getEventResult(event_id);
                                    if (--result < 0)
                                        result = gtengine::FinishResult::RANK_4;
                                
                                    GAME_STATUS.user_profile.record.event_record.updateResultOnly(event_id, result);
                                }
                            }

                            w.init(context, gp, w.event_index, w.is_championship_item, EventSelect.can_take_spot_race);
                        }

                        return EVENTRESULT_FILTER;
                        break;

                    case CELL_PAD_CTRL_L3_UP:
                        if (scrollclip.manualTargetIncY(line_height))
                            return EVENTRESULT_FILTER;
                        break;

                    case CELL_PAD_CTRL_L3_DOWN:
                        if (scrollclip.manualTargetIncY(-line_height))
                            return EVENTRESULT_FILTER;
                        break;
                }

                return EVENTRESULT_CONTINUE;
            }
        }

        module Template
        {
            module EventItem
            {
                attribute course_info;
                attribute event_index;
                attribute is_championship_item = false;
                attribute by_section;

                method onInitialize(context)
                {
                    self.Stars::HBox.clearChildren(context);
                    self.CourseLogo.image_path = "";
                    self.Trophy.image_path = "";
                    self.Result.text = "";
                    self.RaceType.text = "";
                    self.Spot.text = "";
                    self.Arrow.visible = false;
                    self.PassedRegulation.active = false;
                    self.active2 = true;
                }

                method init(context, gp, event_index, is_championship_item, can_take_spot_race)
                {
                    var event = gp.events[event_index];

                    var event_id = is_championship_item ? gp.folder_id : event.event_id;
                    var rp = event.race_parameter;
                    self.event_index = event_index;

                    if (is_championship_item)
                    {
                        self.active3 = true;
                        self.Championship::AllRaces.text = manager.translate("Unit", "RACE_SERIES_COUNT").build(gp.events.size);
                        self.RaceType.text = RaceEventUtil::getGameModeText(context, event);
                        self.is_championship_item = true;
                        self.event_index = 0;
                    }
                    else
                    {
                        self.active3 = false;
                        
                        var course_code = event.track.course_code;
                        var course_info = main::SPECDB.query1("SELECT ModelName, Logo, Name, Reverse, Loop, Scenery FROM COURSE WHERE ID=?", [course_code]);

                        var by_section = rp.complete_type == gtengine::CompleteType::BYSECTION;
                        self.by_section = by_section;
                        self.course_info = course_info;

                        var map_face = Map::CourseMapFace;
                        CourseMapFaceUtil::initialize(context, map_face, course_info, event);
                        map_face.span.pointer_visible = false;
                        map_face.span.move_visible = false;

                        var end_load_courselogo = method (context)
                        {
                            self.CourseLogo.FadeActor.restart();
                            self.CourseLogo.on_delay_load_complete = nil;
                        };

                        self.CourseLogo.on_delay_load_complete = *self.*end_load_courselogo;
                        self.CourseLogo.image_path = "piece/gt6/course_logo_M/%s.img".format(course_info.Logo);

                        var event_stars = event.reward.star_table.size;
                        var current_stars = GAME_STATUS.user_profile.record.event_record.getStars(nil, nil, event_id);

                        self.Stars::HBox.clearChildren(context);

                        for (var i = 0; i < event_stars; i++)
                        {
                            var w = CommonTemplate::Star.doCopy();
                            w.active = i < current_stars;
                            self.Stars::HBox.appendChild(context, w);
                        }

                        self.RaceType.text = RaceEventUtil::getGameModeText(context, event);
                        
                        var is_oneway = !course_info.Loop;
                        var complete_type_txt = RaceEventUtil::getCompleteTypeText(context, event, is_oneway);

                        if (complete_type_txt != "")
                            self.RaceType.text += " / " + complete_type_txt;

                        if (gp.championship)
                        {
                            self.Spot.text = manager.translate("Unit", "RACE_NO").build(event_index + 1);
                            Arrow.visible = event_index > 0;
                            self.active2 = can_take_spot_race == true;
                        }
                    }

                    var result = RaceEventUtil::getResult(event_id);
                    var order = result + 1;
                    if (order >= 1 && order <= 3)
                    {
                        var rank = ["gold", "silver", "bronze"][result];
                        self.Trophy.image_path = "/image/gt6/event/trophy_%{rank}.dds";
                        self.Result.text = "";
                    }
                    else if (order >= 4)
                    {
                        self.Trophy.image_path = "";
                        self.Result.text = result + 1;
                    }
                }

                method updateRegulation(passed_regulation)
                {
                    self.PassedRegulation.active = passed_regulation;
                }

                method focus_enter(context)
                {
                    if (is_championship_item)
                    {
                        EventSelect::EventFolderName::Course.text = "";
                    }
                    else
                    {
                        var map_face = Map::CourseMapFace;
                        if (self.by_section)
                        {
                            map_face.span.pointer_visible = true;
                            map_face.span.move_visible = true;
                        }
                        else
                        {
                            map_face.span.pointer_visible = true;
                            map_face.span.move_visible = false;
                        }

                        map_face.span.SpanActor.restart();
                        
                        var course_name = manager.translate("CourseName", self.course_info.Name);
                        if (self.course_info.Reverse)
                            course_name += " / " + manager.translate("CourseData", "direction_reverse");

                        EventSelect::EventFolderName::Course.text = " : %{course_name}";
                    }
                }

                method focus_leave(context)
                {
                    if (!is_championship_item)
                    {
                        var map_face = Map::CourseMapFace;
                        map_face.span.pointer_visible = false;
                        map_face.span.move_visible = false;
                        map_face.span.SpanActor.reset();
                    }
                }
            }
        }
    }
}