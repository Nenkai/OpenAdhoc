//---------------------------------------------------------------------------------------
// QuickTunePopup.ad
//
// Arcade mode Pre-race Menu: Quick Tune menu
// 
//---------------------------------------------------------------------------------------


//---------------------------------------------------------------------------------------
// module
//---------------------------------------------------------------------------------------

module PROJECT::ROOT
{
	#include "../quick-share/quick_tune_popup.ad";

#ifdef GT4O_US_BETA
	static originalPower = nil;
	static originalWeight = nil;
	
	static powerLevel = nil;
	static weightLevel = nil;
	
	static gearMaxSpeed = nil;
	static gearMaxSpeed_min = nil;
	static gearMaxSpeed_max = nil;
	
	static frontTireType = nil;
	static rearTireType = nil;
	
	static transmission = nil;
	static driving_assist = nil;
	
	static CarType = nil;
	static TireGroupIndex = nil;
#else
	static originalPower;
	static originalWeight;
	
	static powerLevel;
	static weightLevel;
	
	static gearMaxSpeed;
	static gearMaxSpeed_min;
	static gearMaxSpeed_max;
	
	static frontTireType;
	static rearTireType;
	
	static transmission;
	static driving_assist;
	
	static CarType;
	static TireGroupIndex;
#endif

	function onInitialize(context)
	{
		var quick_work = main::menu::MQuickWork();
	
		Hidden.visible = false;
	
		store(context);
	
		ROOT::Pane::Info::PowerUnit.text = main::menu::MUnit::GetUnitOfPower();
	
		if (frontTireType >= 6 && frontTireType <= 10)
		{
			CarType = 1;
		}
		else
		{
			CarType = 0;
		}
	
		Pane::Power::slider.index = powerLevel;
		Pane::Weight::slider.index = weightLevel;
	
		setupSlider(Pane::Power::slider);
		setupSlider(Pane::Weight::slider);
	
		Pane::GearMaxSpeed::slider.range = [gearMaxSpeed_min, gearMaxSpeed_max];
		Pane::GearMaxSpeed::slider.index = gearMaxSpeed;
	
		setupSlider_MaxSpeed(Pane::GearMaxSpeed::slider);
	
		initTransmissionWidget(context);
	
		initDrivingAssistWidget(context, isDirt(main::game.course_code));
	
		initTireWidget(context);
	
		refrect(context);
	}

	function open(context)
	{
		context.startPage(ROOT);
	}
	
	function close(context)
	{
		context.startPage(ArcadeRoot);
	}

	function onFinalize(context)
	{
		Pane::Tire::OptionMenu::popup::vbox.clearWindow(context);
		Pane::Tire::Front::popup::vbox.clearWindow(context);
		Pane::Tire::Rear::popup::vbox.clearWindow(context);
		Pane::TransMission::OptionMenu::popup::vbox.clearWindow(context);
		Pane::DrivingAssist::OptionMenu::popup::vbox.clearWindow(context);
	}

	function onCancel(context)
	{
		main::sound.play("cancel");
		close(context);
		return EVENTRESULT_FILTER;
	}
}

module PROJECT::ROOT::Pane::Power::slider
{
	function onValueChanged(context)
	{
		setupSlider(slider);
		ROOT::powerLevel = slider.index;
		refrect(context);
	}
}

module PROJECT::ROOT::Pane::Weight::slider
{
	function onValueChanged(context)
	{
		setupSlider(slider);
		ROOT::weightLevel = slider.index;
		refrect(context);
	}
}

module PROJECT::ROOT::Pane::GearMaxSpeed::slider
{
	function onValueChanged(context)
	{
		setupSlider_MaxSpeed(slider);
		ROOT::gearMaxSpeed = slider.index;
	}
}

module PROJECT::ROOT::Pane::DrivingAssist::OptionMenu
{
	function onValueChanged(context)
	{
		driving_assist = ROOT::Pane::DrivingAssist::OptionMenu.index;
	}
}

module PROJECT::ROOT::Pane::TransMission::OptionMenu
{
	function onValueChanged(context)
	{
		transmission = ROOT::Pane::TransMission::OptionMenu.index;
	}
}

module PROJECT::ROOT::Pane::Tire::OptionMenu
{
	function onValueChanged(context)
	{
		if (TireGroupIndex == ROOT::Pane::Tire::OptionMenu.index)
		{
			return;
		}
	
		TireGroupIndex = ROOT::Pane::Tire::OptionMenu.index;
	
		ROOT::Pane::Tire::Front::popup::vbox.clearWindow(context);
		ROOT::Pane::Tire::Rear::popup::vbox.clearWindow(context);
	
		if (ROOT::Pane::Tire::OptionMenu.index == 0)
		{
			initialize_optionmenu(context, ROOT::Pane::Tire::Front, tire_category_normal);
			initialize_optionmenu(context, ROOT::Pane::Tire::Rear, tire_category_normal);
		}
		else
		{
			initialize_optionmenu(context, ROOT::Pane::Tire::Front, tire_category_sports);
			initialize_optionmenu(context, ROOT::Pane::Tire::Rear, tire_category_sports);
		}
	
		ROOT::Pane::Tire::Front.setIndex(context, 0);
		ROOT::Pane::Tire::Rear.setIndex(context, 0);
	}
}

module PROJECT::ROOT::Pane::Buttons::Cancel
{
	function onActivate(context)
	{
		main::sound.play("ok");
		close(context);
		return EVENTRESULT_FILTER;
	}
}

module PROJECT::ROOT::Pane::Buttons::OK
{
	function onActivate(context)
	{
		main::sound.play("ok");
		apply(context);
		close(context);
		return EVENTRESULT_FILTER;
	}
}