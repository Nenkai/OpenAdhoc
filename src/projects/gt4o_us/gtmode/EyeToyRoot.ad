//---------------------------------------------------------------------------------------
// EyeToyRoot.ad
//
// Car modal with car specs & details when buying a car
// 
//---------------------------------------------------------------------------------------


//---------------------------------------------------------------------------------------
// module
//---------------------------------------------------------------------------------------

#define NIKE_ONE "nike_one_04"

module PROJECT::ROOT
{
#ifdef GT4O_US_BETA
    static gtshirt = nil;
    static recognized = nil;
#else
    static gtshirt;
    static recognized;
#endif

    function setMoveActor(widget, x, y)
    {
        var actor = main::menu::MMoveActor(widget, widget.x + x, widget.y + y);
        widget.actor = actor;
        widget.actor.repeat = false;
        widget.actor.linear = true;
        widget.actor.ratio = 0.005;
        widget.actor.out = true;
        widget.actor.warp();
    }

    function setFadeInActor(widget, period)
    {
        var actor = main::menu::MFadeActor(widget, 1.0);
        actor.repeat = false;
        actor.period = period;
        widget.actor = actor;
        widget.actor.out = true;
        widget.actor.warp();
    }

    setMoveActor(TShirtLogo::bg0, -128.0, -128.0);
    setMoveActor(TShirtLogo::bg1, 128.0, -128.0);
    setMoveActor(TShirtLogo::bg2, -128.0, 128.0);
    setMoveActor(TShirtLogo::bg3, 128.0, 128.0);
    setFadeInActor(TShirtLogo, 1.0);
    setFadeInActor(Logo, 3.0);
    setFadeInActor(carbg, 3.0);
    setFadeInActor(Pane, 1.0);
    setFadeInActor(Veil, 3.0);

    function onInitialize(context)
    {
        context.cursor_visible = false;
        enterPage(context, ROOT, "Nike");
        Eyetoy.visible = true;
        Frame.visible = true;

        TShirtLogo::bg0.actor.out = true;
        TShirtLogo::bg0.actor.warp();

        TShirtLogo::bg1.actor.out = true;
        TShirtLogo::bg1.actor.warp();

        TShirtLogo::bg2.actor.out = true;
        TShirtLogo::bg2.actor.warp();

        TShirtLogo::bg3.actor.out = true;
        TShirtLogo::bg3.actor.warp();

        TShirtLogo.actor.out = true;
        TShirtLogo.actor.warp();

        Logo.actor.out = true;
        Logo.actor.warp();

        Veil.visible = false;
        Veil.actor.out = true;
        Veil.actor.warp();

        carbg.visible = false;
        carbg.actor.out = true;
        carbg.actor.warp();

        CarFace.visible = false;
        CarFace.action_type = "normal";
        CarFace.quality_level = 1;

        Logo.visible = false;
        TShirtLogo.visible = false;
        Pane.visible = false;

        Pane::label.text = context.translate(ROOT, "you got a car").format(main::menu::MCarData::GetCarName(NIKE_ONE));
        gtshirt = main::menu::MGTShirt();
        recognized = false;
        
        var eyetoy_face = EyetoyRoot::Eyetoy::eyetoy;
        eyetoy_face.processor = gtshirt;
        eyetoy_face.mirror = true;

        var camera = eyetoy_face.device;
        camera.mirror = false;
        gtshirt.threshold = 64;
        gtshirt.threshold_pattern_base = 0.2;
        camera.start_camera();

        context.pushEvent(main::menu::MScriptEvent(context, ROOT, "recognizeStart"));
    }

    function onFinalize(context)
    {
        var camera = EyetoyRoot::Eyetoy::eyetoy.device;
        camera.led_on = false;
        camera.led_off = true;
        camera.stop_camera();

        gtshirt = nil;
        recognized = nil;

        context.cursor_visible = true;
        leavePage(context, ROOT);
    }

    function onCancel(context)
    {
        main::sound.play("cancel");
        var dealer = main::game.menuPop();
        set_last_focus(dealer);
        start_page(context, NationRoot);
        return EVENTRESULT_FILTER;
    }

    function recognizeStart(context)
    {
        context.sync(1.0);
        gtshirt.recognizing = true;
    }

    // Fired when recognition is successful
    function getting_action(context)
    {
        // Play cursor sound & Wait 3.25s, center all shirt symbols on the screen
        main::sound.play("cursor");

        TShirtLogo.visible = true;

        TShirtLogo::bg0.actor.out = false;
        TShirtLogo::bg1.actor.out = false;
        TShirtLogo::bg2.actor.out = false;
        TShirtLogo::bg3.actor.out = false;
        TShirtLogo.actor.out = false;

        Veil.visible = true;
        Veil.actor.out = false;

        context.sync(3.25);

        // Play sound, wait 2 seconds
        main::sound.play("ok");
        context.sync(2.0);

        // Fade symbols to Nike, and GT Logo for two seconds
        TShirtLogo.actor.out = true;
        Logo.visible = true;
        Logo.actor.out = false;
        context.sync(2.0);
        
        // Stay still for 5 seconds
        main::sound.play("return");
        context.sync(5.0);

        // Unlock car & begin car unveil for 4 seconds - jingle start playing
        var car = NIKE_ONE;
        main::game.setCarAvailable(car);
        main::game.garage.addNewCar(car, 0);
        main::game.calendar.putPresentCarEvent(car, 0);

        Eyetoy.visible = false;
        Frame.visible = false;
        carbg.visible = true;
        carbg.actor.out = false;
        main::sound.startStream("pcm", "get_car");
        context.sync(4.0);

        // Car is on screen, start rotating it for another 5 seconds (jingle drop happens)
        CarFace.visible = true;
        CarFace.car_code = car;
        CarFace::move_car(0, 1);
        context.sync(5.0);

        // Show car name "ok" button
        main::sound.play("next");
        Pane.visible = true;
        Pane.actor.out = false;
        ROOT.setFocus(Pane::OK);
        context.cursor_visible = true;
        context.sync(2.0);

        // Done
        context.flushKeyEvent();
    }
}

module PROJECT::ROOT::Eyetoy::eyetoy
{
    function onGTShirtFocus(context)
    {

    }

    function onGTShirtUnfocus(context)
    {

    }

    function onGTShirtRecognized(context, x, y, w, h)
    {
        if (context.time_after_last_input > 4.0 && !EyetoyRoot::recognized)
        {
            EyetoyRoot::recognized = true;
            EyetoyRoot::gtshirt.recognizing = false;
            context.pushEvent(main::menu::MScriptEvent(context, ROOT, "getting_action"));
        }
    }
}

module PROJECT::ROOT::CarFace
{
    function move_car(phase, warp)
    {
        if (CarFace.car_model != nil)
        {
            if (phase == false)
            {
                CarFace.car_model.motion = 1;
                if (warp == true)
                {
                    CarFace.car_model.tz = 8.5;
                    CarFace.car_model.ry = 180.0;
                    CarFace.car_model.warp();
                }

                CarFace.car_model.tx = 0.0;
                CarFace.car_model.tz = 0.0;
                CarFace.car_model.posInterpolateSpeed = 0.025;
            }
            else
            {
                CarFace.car_model.motion = 2;
                CarFace.car_model.tx = -0.9;
                CarFace.car_model.tz = 1.6;
                CarFace.car_model.ry = 185.5;
                CarFace.car_model.posInterpolateSpeed = 0.1;

                if (warp == true)
                    CarFace.car_model.warp();
            }
        }
    }
}

module PROJECT::ROOT::Pane::OK
{
    function onActivate(context, event)
    {
        main::sound.play("ok");
        main::sound.stopStream();
        start_page(context, DealerRoot);
        return EVENTRESULT_FILTER;
    }
}