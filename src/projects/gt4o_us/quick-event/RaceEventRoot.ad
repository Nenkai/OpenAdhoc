module RaceEventProject
{
    #include "../share/dialog.ad";
    #include "../share/page_util.ad";
    #include "../share/race_util.ad";
    #include "../share/prize_action.ad";

    function getStartPage()
    {
        var quick_work = main::menu::MQuickWork();

        if (quick_work.IsSessionFinished)
        {
            if (quick_work.prize != 0)
                return PrizeRoot;
            else
                return AfterRoot;
        }
        return RaceEventRoot;
    }

    function onLoad(context)
    {
        var quick_work = main::menu::MQuickWork();

        context.createRenderContext(1);
        var render_context = context.getRenderContext(0);
        render_context.transition = main::menu::MColorTransition();

        render_context.startPage(getStartPage());
    }

    function onUnload(context)
    {
        
    }
}
module RaceEventProject::RaceEventRoot
{
    #include "../quick-share/icon.ad";

    var quick_work = main::menu::MQuickWork();
    for (var i = 1; i <= quick_work.numberOfEntries; i++)
    {
        var widget = ROOT["Grid" + i];
        setMoveActor(widget, 640 + (392 * 2 * i), 0, 0.15);
    }

    function onInitialize(context)
    {
        var quick_work = main::menu::MQuickWork();

        for (var i = 0; i < quick_work.numberOfEntries; i++)
        {
            ROOT["Grid" + (i + 1)]["CarName"].text = quick_work.getGridCarName(i);
            ROOT["Grid" + (i + 1)]["CarName"].abbreviate();
            ROOT["Grid" + (i + 1)]["ColorChip"].setColor(quick_work.getColorChipInfo(i));
        }

        for (var i = 1; i <= quick_work.numberOfEntries; i++)
        {
            var widget = ROOT["Grid" + i];
            widget.actor.out = true;
            widget.actor.warp();
            widget.actor.out = false;
        }

        for (var i = quick_work.numberOfEntries + 1; i <= 6; i++)
        ROOT["Grid" + i].visible = false;

        if (quick_work.playerGridNumber >= 0)
        {
            var gnum = quick_work.playerGridNumber + 1;
            ROOT["Grid" + gnum]["CarName"].color = ROOT::Hidden::MyGridColor.color;
            ROOT["Grid" + gnum]["GridNumber"].color = ROOT::Hidden::MyGridColor.color;
        }

        var race_code = main::menu::MQuickWork().raceLabel;

        if (raceLabel == "family")
            GridTitle_text.text = context.translate(ROOT, "EventName", race_code);
        else
            GridTitle_text.text = getEventNameByRaceCode(context, ROOT, race_code);

        GridTitle_text.adjustScale();

        var unit = main::game.race_record.getUnit(race_code);

        Score.text = context.translate(ROOT, "BestScore").format(unit.best_score);
        Score.adjustScale();

        var course_label = quick_work.courseLabel;
        Title.text = context.translate(ROOT, "CourseName", course_label);
        ROOT::ToolTip_Upper::CourseLength.text = context.translate(ROOT, "course_length").format(main::menu::MUnit::GetCourseLengthString(quick_work.CourseLength));

        if (quick_work.Laps > 1)
        {
            ROOT::ToolTip_Upper::Laps.text = quick_work.Laps;
            ROOT::ToolTip_Upper::Laps.text + context.translate(ROOT, "laps");
        }
        else 
        {
            ROOT::ToolTip_Upper::Laps.text = quick_work.Laps;
            ROOT::ToolTip_Upper::Laps.text + context.translate(ROOT, "lap");
        }

        main::sound.startStreamEx("pcm", "default_quick", -1, 1, main::game.option.menu_bgm_volume);
        setIconPosition(context);
        setDefaultIconFocus(context);
    }

    function set_next_start_project()
    {
        main::game.next_menu_project = main::game.major_menu_project;
    }

    static icon_name_tbl = [
        "Start",
        "Spectator",
        "Setting",
        "Option",
        "Exit"
    ];

    function setIconPosition(context)
    {
        var max = icon_name_tbl.size;
        var num = 0;

        for (var i = 0; i < max; i++)
        {
            if (ROOT::Buttons[icon_name_tbl[i]].visible == true)
                num++;
        }

        var screen_w = 640;
        var icon_w = 72;
        var space = 4;

        var st = ((screen_w - ((num * icon_w))) - (space * (num - 1))) / 2;
        var c = 0;
        
        for (var i = 0; i < max; i++)
        {
            if (ROOT::Buttons[icon_name_tbl[i]].visible == true)
            {
                ROOT::Buttons[icon_name_tbl[i]].x = st + ((icon_w + space) * c);
                c++;
            }
        }
    }

    function onCancel(context, event, item)
    {
        main::sound.play("cancel");

        if (!openConfirmDialog(context, 1, context.translate(ROOT, "do you abandon this race?")))
            return STATE_YIELD;

        main::sound.stopStream();
        quick_work = main::menu::MQuickWork();
        quick_work.selectedCommand = "COMMAND_EXIT";
        
        context.transition.panOut(context);
        context.transition.syncOut(context);

        set_next_start_project();
        context.finish();
        return STATE_YIELD;
    }
}

module RaceEventProject::RaceEventRoot::Buttons::Start
{
    function onActivate(context)
    {
        var quick_work = main::menu::MQuickWork();

        if (!quick_work.IsASpec)
        {
            main::sound.play("disable");
            openConfirmDialog(context, 2, context.translate(ROOT, "no race since you donot have license"));
            return STATE_YIELD;
        }

        main::sound.play("ok");
        main::sound.stopStream();
        quick_work.selectedCommand = "COMMAND_START";
        quick_work.cursorPosition = 0;

        context.transition.panOut(context);
        context.transition.syncOut(context);
        context.finish();

        return STATE_YIELD;
    }
}

module RaceEventProject::RaceEventRoot::Buttons::Spectator
{
    function onActivate(context)
    {
        var quick_work = main::menu::MQuickWork();

        if (quick_work.raceLabel.substr(0, 3) == "rh_")
        {
            main::sound.play("disable");
            openConfirmDialog(context, 2, context.translate(ROOT, "no BSpec for this race"));
            return STATE_YIELD;
        }

        main::sound.play("ok");
        main::sound.stopStream();
        quick_work.selectedCommand = "COMMAND_SPECTATOR";
        quick_work.cursorPosition = 0;

        context.transition.panOut(context);
        context.transition.syncOut(context);
        set_next_start_project();
        context.finish();

        return STATE_YIELD;
    }
}

module RaceEventProject::RaceEventRoot::Buttons::Setting
{
    function onActivate(context)
    {
        main::sound.play("ok");
        main::sound.stopStream();

        var quick_work = main::menu::MQuickWork();
        quick_work.selectedCommand = "COMMAND_SETTINGS";
        quick_work.cursorPosition = getIconIndex(context, "Setting");

        context.transition.panOut(context);
        context.transition.syncOut(context);
        set_next_start_project();
        context.finish();

        return STATE_YIELD;
    }
}

module RaceEventProject::RaceEventRoot::Buttons::Option
{
    function onActivate(context)
    {
        main::sound.play("ok");
        main::sound.stopStream();

        var quick_work = main::menu::MQuickWork();
        quick_work.selectedCommand = "COMMAND_OPTION";
        quick_work.cursorPosition = getIconIndex(context, "Option");

        context.transition.panOut(context);
        context.transition.syncOut(context);
        set_next_start_project();
        context.finish();

        return STATE_YIELD;
    }
}

module RaceEventProject::RaceEventRoot::Buttons::Exit
{
    function onActivate(context)
    {
        main::sound.play("ok");

        if (!openConfirmDialog(context, 1, context.translate(ROOT, "do you abandon this race?")))
            return STATE_YIELD;

        main::sound.stopStream();
        var quick_work = main::menu::MQuickWork();
        quick_work.selectedCommand = "COMMAND_EXIT";
        
        context.transition.panOut(context);
        context.transition.syncOut(context);

        set_next_start_project();
        context.finish();
        return STATE_YIELD;
    }
}