module ROOT
{
    attribute popup;
    attribute closed = false;
    attribute title;

    attribute category_default;
    attribute category_custom;
    attribute sysbgm_context;
    attribute custom_selectable;
    attribute custom_enable;
    attribute restore_func;
    attribute result;

    method open(context, title, category_default_key, category_custom_key, custom_selectable, custom_enable, restore_func)
    {
        self.result = nil;
        self.title = title;

        self.category_default = category_default_key;
        self.category_custom = category_custom_key;
        self.sysbgm_context = nil;
        self.custom_selectable = custom_selectable;
        self.custom_enable = custom_enable;

        self.restore_func = restore_func;
        self.closed = false;
        self.popup = main::SequenceUtil::ModalPage(context, ROOT);
        self.popup.open();

        return [self.result, self.sysbgm_context];
    }

    method close(context)
    {
        if (self.closed)
            return;

        self.closed = true;
        ContextMain.event_mask = 0x05;
        ContextMain.disableInput();

        FadeActor.on_effect_end = function (context)
        {
            if (ROOT.popup != nil)
            {
                ROOT.popup.close();
                ROOT.popup = nil;
            }

            ContextMain.enableInput();
        }

        FadeActor.out = true;
    }

    method onInitialize(context)
    {
        Popup::Pane::TitleRow::label.text = context.translate(ROOT, "MyHome", self.title);

        var category_name = context.translate(ROOT, "MyHome", self.category_default);
        var category_custom_name = context.translate(ROOT, "MyHome", self.category_custom);

        Popup::Pane::Body::SelectCustom::Label.text = category_custom_name;
        var checkbox = Popup::Pane::Body::UseCustom::Box::CheckBox;

        checkbox.active = self.custom_enable;
        checkbox.insensitive = !self.custom_selectable;

        Popup::Pane::Body::Playlist::Label.text = category_name;
        ROOT.setFocus(Popup::Pane::FootRow::CancelOK::OK);

        return EVENTRESULT_FILTER;
    }

    method onFinalize(context)
    {

    }

    method onCancel(context)
    {
        main::sound.play("cancel");
        close(context);

        return EVENTRESULT_FILTER;
    }

    module Popup::Pane
    {
        module Body
        {
            module SelectCustom::Box::Circle
            {
            
                method onActivate(context)
                {
                    main::sound.play("ok");
                    SoundUtil::SetSystemBGMEnable(context, true);
                
                    var bytedata = SoundUtil::OpenSystemBGMDialog(context);
                
                    if (bytedata != nil)
                    {
                        ROOT.sysbgm_context = bytedata;
                        var checkbox = Popup::Pane::Body::UseCustom::Box::CheckBox;
                        checkbox.insensitive = false;
                    }
                
                    return EVENTRESULT_FILTER;
                }
            }

            module Playlist::RestoreDefault::Circle
            {
            
                method onActivate(context)
                {
                    main::sound.play("ok");
                
                    var category_name = context.translate(ROOT, "MyHome", ROOT.category_default);
                    var text = context.translate(ROOT, "RESTORE_DEFAULT_PLAYLIST_CONFIRM").build(category_name);
                    var result = DialogUtil::openConfirmDialog(context, DialogUtil::DEFAULT_NO, text);
                
                    if (result)
                        ROOT.restore_func();
                
                    return EVENTRESULT_FILTER;
                }    
            }
        }

        module FootRow::CancelOK
        {
            module Cancel
            {
                method onActivate(context)
                {
                    main::sound.play("ok");
                    ROOT.close(context);
                
                    return EVENTRESULT_FILTER;
                }    
            }

            module OK
            {
                method onActivate(context)
                {
                    main::sound.play("ok");
                    ROOT.result = Popup::Pane::Body::UseCustom::Box::CheckBox.active;
                    ROOT.close(context);
                
                    return EVENTRESULT_FILTER;
                }    
            }
        }
    }
}