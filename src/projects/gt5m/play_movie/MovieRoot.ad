//---------------------------------------------------------------------------------------
// MovieRoot.ad
//
//
// 
//---------------------------------------------------------------------------------------


//---------------------------------------------------------------------------------------
// module
//---------------------------------------------------------------------------------------

module ROOT
{
    attribute caption_list = nil;
    attribute watcher = nil;
    attribute increment_y = nil;

    hidden.visible = false;

    method open(context, file_name, caption_list)
    {
        SoundUtil::BGMFadeout(0.5);

        self.caption_list = caption_list;

        MovieFace.audio_level = SoundUtil::GetMovieVolume();
        MovieFace.file_name = file_name;
        SequenceUtil::startPage(context, ROOT);
    }

    method close(context)
    {
        main::SequenceUtil::startProject(context, main::after_boot_project);
    }

    method onInitialize(context)
    {
        MovieFace.play_movie(context);

        if (nil != self.caption_list)
        {
            ScrollClip.scroll_mode = 2;
            ScrollClip.space.y = 0.0;
            ScrollClip.manual_target_y = 0.0;

            var dummy_text = "\n\n\n\n\n\n\n\n\n\n\n\n\n\n\n\n\n\n\n\n\n\n\n\n\n\n";
            var list = [];
            list.push(dummy_text);
            foreach (var caption in self.caption_list)
                list.push(caption);
            list.push(dummy_text);

            foreach (var caption in list)
            {
                var item = hidden::TextFace.doCopy();
                item.text = caption;
                ScrollClip::space.appendChild(context, item);
            }
            ScrollClip.updateGeometry();

            if (self.watcher == nil)
            {
                self.watcher = main::WatcherUtil::Create(context, onTick, 5);
                self.increment_y = -3;
            }
        }

        return EVENTRESULT_FILTER;
    }

    method onFinalize(context)
    {
        MovieFace.stop_movie(context);
        deleteWatcher(context);

        return EVENTRESULT_FILTER;
    }

    method onActivate(context)
    {
        main::sound.play("ok");
        MovieFace.end_movie(context);
        return EVENTRESULT_FILTER;
    }

    method onCancel(context)
    {
        main::sound.play("cancel");
        MovieFace.end_movie(context);
        return EVENTRESULT_FILTER;
    }

    method onKeyPress(context, event)
    {
        switch (event.keysym)
        {
            case PSP_PAD_CTRL_START:
                main::sound.play("ok");
                MovieFace.end_movie(context);
                return EVENTRESULT_FILTER;
        }

        return EVENTRESULT_CONTINUE;
    }

    method onTick(context)
    {
        var result = ScrollClip.manualTargetIncY(self.increment_y);
        if (!result)
            deleteWatcher(context);
    }

    method deleteWatcher(context)
    {
        if (self.watcher != nil)
        {
            main::WatcherUtil::Delete(self.watcher);
            self.watcher = nil;
        }
    }

    module MovieFace
    {
        attribute file_name = nil;
        attribute is_finish = false;

        method play_movie(context)
        {
            SoundUtil::BGMStop();

            self.is_finish = false;
            self.movie_name = self.file_name;
            self.unload();
            self.load(context, self.file_name);
        }

        method stop_movie(context)
        {
            self.unload();
            self.is_finish = false;
        }

        method end_movie(context)
        {
            self.pause(true);
            context.wait();
            context.wait();
            self.is_finish = nil;
            ROOT.close(context);
        }


        method onMovieFinish(context)
        {
            if (ROOT.isClosed())
                return;

            if (self.is_finish != nil && !self.is_finish)
            {
                self.is_finish = true;
                self.end_movie(context);
            }
        }
    }
}