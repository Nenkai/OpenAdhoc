




















static MenuItemNONE = 0;
static MenuItemTEXT = 1;
static MenuItemSLIDER = 2;
static MenuItemINPUT = 3;




class MenuItem
{
    attribute type = nil;
    attribute label = nil;
    attribute tip = nil;
    attribute on_init_func = nil;
    attribute on_update_func = nil;
    attribute on_activate_func = nil;
    attribute on_keypress_func = nil;
    attribute arg = nil;
    attribute base_color = nil;

    method __init__(_type, _label, _tip, _init_func, _update_func, _activate_func, _keypress_func, _arg)
    {
        self.type = _type;
        self.label = _label;
        self.tip = _tip;
        self.on_init_func = _init_func;
        self.on_update_func = _update_func;
        self.on_activate_func = _activate_func;
        self.on_keypress_func  = _keypress_func;
        self.arg = _arg;
        self.base_color = nil;
    }
}





class SelectMenuData
{
    attribute target_ref = nil;
    attribute candidates = nil;
    attribute none_txt = nil;
    attribute on_decide_func = nil;

    method __init__(target_ref, candidates)
    {
        self.target_ref = target_ref;
        self.candidates = candidates;
        self.none_txt = nil;
        self.on_decide_func = nil;
    }
}



class SliderMenuData
{
    attribute target_ref = nil;
    attribute range = nil;
    attribute step = nil;
    attribute bias = nil;
    attribute zero_text = nil;
    attribute unit_text = nil;
    attribute bar_on = nil;
    attribute step_thr = nil;

    method __init__(target_ref, range, zero_text, unit_text)
    {
        self.target_ref = target_ref;
        self.range = range;
        self.step = 1;
        self.bias = 1;
        self.zero_text = zero_text;
        self.unit_text = unit_text;
        self.bar_on = true;
        self.step_thr = 10;
    }
}




class ToggleMenuData 
{
    attribute target_ref = nil;
    attribute candidates = nil;
    method __init__(target_ref, candidates)
    {
        self.target_ref = target_ref;
        self.candidates = candidates;
    }
}





class InputMenuData
{
    attribute value = nil;
    attribute range = nil;
    attribute close_cb = nil;
    attribute return_cb = nil;

    method __init__(default_val, range, close_cb, return_cb)
    {
        self.value = default_val;
        self.range = range;
        self.close_cb = close_cb;
        self.return_cb = return_cb;
    }
}



module RunviewerUtil
{
    function fileToList(path, need_pair)
    {
        if (need_pair == nil)
            need_pair = false;

        var st = main::pdistd::ReadFile(path);
        if (st["result"] == 0)
        {
            var buf = st["buffer"];
            buf = buf.toString();

            var splitter;
            if (buf.find("\r\n") != -1)
                splitter = "\r\n";
            else if (buf.find("\n") != -1)
                splitter = "\n";
            else if (buf.find("\r") != -1)
                splitter = "\r";

            var lines = buf.split(splitter);
            var list = [];
            foreach (var line in lines)
            {
                line = line.chop();
                line = line.trim();
                if (line.beginsWith("#"))
                    continue;
                if (line.size == 0)
                    continue;
                if (need_pair)
                    list.push([line, nil]);
                else
                    list.push(line);
            }
            return list;
        }
        else
        {
            return nil;
        }
    }

    function SearchFocusIndex(value, list)
    {
        if (list == nil)
            return 0;

        var focus_index = 0;
        for (var i = 0; i < list.size; i++)
        {
            if (value == list[i].label.toString())
            {
                focus_index = i;
                break;
            }
        }

        return focus_index;
    }

    function on_select_jumpmenu(context, index, menuitem, item, arg)
    {
        var smdata = arg[0];
        SoundRoot.open_menu(context, menuitem.label, arg, index, 0);
    }

    function CreateJump(title, tip, menuitems)
    {
        var jump_menuitem = MenuItem(MenuItemTEXT, title, tip, nil, nil, on_select_jumpmenu);
        jump_menuitem.arg = menuitems;
        return jump_menuitem;
    }

    function on_update_selectmenu(context, index, menuitem, item)
    {
        var smdata = menuitem.arg[0];
        foreach (var it in smdata.candidates)
        {
            if (it[0] == *smdata.target_ref)
            {
                var value = it[1];
                if (value == nil)
                    value = it[0];

                if (value == "none" && smdata.none_txt != nil)
                    value = smdata.none_txt;
                item["value"].text = value;
                break;
            }
        }
    }

    function on_select_selectmenu(context, index, menuitem, item, arg)
    {
        var smdata = arg[0];
        SoundRoot.open_menu(context, menuitem.label, arg[1], index, SearchFocusIndex(*smdata.target_ref, arg[1]));
    }

    function on_update_sub_selectmenu(context, index, menuitem, item)
    {
        var smdata = menuitem.arg;
        var col;
        var label = menuitem.label;
        if (label == *smdata.target_ref)
            col = main::menu::MColorObject(1.0, 1.0, 0.0);
        else
        {
            col = main::menu::MColorObject(1.0, 1.0, 1.0);
        }
        item["label"].text_color = col;
        item["value"].text_color = col;

        var value = smdata.candidates[index][1];
        if (value == nil)
        {
            item["label"].text = "";
            value = label;
        }

        if (value == "none" && smdata.none_txt != nil)
            value = smdata.none_txt;
        item["value"].text = value;
    }

    function on_select_sub_selectmenu(context, index, menuitem, item, arg)
    {
        var smdata = arg;
        *smdata.target_ref = menuitem.label;
        if (smdata.on_decide_func != nil)
        {
            smdata.on_decide_func(context, *smdata.target_ref);
        }

        SoundRoot.close_menu(context);
    }

    function CreateSelect(target_ref, title, tip, candidates, none_txt, decide_func)
    {
        var select_menuitem = MenuItem(MenuItemTEXT, title, tip, nil, on_update_selectmenu, on_select_selectmenu);
        var smdata = SelectMenuData(target_ref, candidates);

        smdata.none_txt = none_txt;
        smdata.on_decide_func = decide_func;

        var menuitems = [];

        for (var i = 0; i < candidates.size; i++)
        {
            var label = candidates[i][0];
            var menuitem = MenuItem(MenuItemTEXT, label, "", nil, on_update_sub_selectmenu, on_select_sub_selectmenu);
            menuitem.arg = smdata;
            menuitems.push(menuitem);
        }
        select_menuitem.arg = [smdata, menuitems];
        return select_menuitem;
    }

    ////////////////////////////////////////
    ////////// Sliders
    ////////////////////////////////////////

    function on_init_slider(context, menuitem, item)
    {
        var slider = item.slider;
        var smdata = menuitem.arg;
        slider.range = smdata.range;
        slider.step = smdata.step;
        slider.value = *smdata.target_ref;
        slider.setup(context, smdata.bias, smdata.zero_text, 
            smdata.unit_text, smdata.bar_on, smdata.step_thr);
        slider.decide_cb = menuitem.on_activate_func;
        slider.arg = smdata;
    }

    function on_activate_slider(context, value, slider)
    {
        var smdata = slider.arg;
        *smdata.target_ref = value;
    }

    function CreateSlider(target_ref, title, tip, range, zero_text, unit_text)
    {
        var smdata = SliderMenuData(target_ref, range, zero_text, unit_text);
        var slider_menuitem = MenuItem(MenuItemSLIDER, title, tip, 
            on_init_slider, nil, on_activate_slider);
        slider_menuitem.arg = smdata;
        return slider_menuitem;
    }

    ////////////////////////////////////////
    ////////// Toggles
    ////////////////////////////////////////


    function on_update_select_toggle(context, index, menuitem, item)
    {
        var tmdata = menuitem.arg[0];
        item["value"].text = tmdata.candidates[*tmdata.target_ref][1];
    }

    function on_select_toggle(context, index, menuitem, item, arg)
    {
        |var tmdata, var decideCB| = arg;
        var i = 0;
        while (i < tmdata.candidates.size)
        {
            if (tmdata.candidates[i][0] == *tmdata.target_ref)
            {
                i++;
                break;
            }
            i++;
        }
        if (i >= tmdata.candidates.size)
            i = 0;
        *tmdata.target_ref = i;
        item["value"].text = tmdata.candidates[*tmdata.target_ref][1];
        if (decideCB != nil)
            decideCB(context, i);
    }

    function CreateToggle(target_ref, title, tip, candidates, decideCB)
    {
        var tmdata = ToggleMenuData(target_ref, candidates);
        var select_toggle_menuitem = MenuItem(MenuItemTEXT, title, tip, nil, 
            on_update_select_toggle, on_select_toggle);
        select_toggle_menuitem.arg = [tmdata, decideCB];
        return select_toggle_menuitem;
    }

    ////////////////////////////////////////
    ////////// Text Input
    ////////////////////////////////////////
    function on_init_input(context, menuitem, item)
    {
        var imdata = menuitem.arg;
        item.Input.visible = true;
        item.Input.input.value = imdata.value;
        item.Input.input.close_cb = imdata.close_cb;
        item.Input.input.return_cb = imdata.return_cb;
    }

    function CreateInput(title, tip, value, range, close_cb, return_cb)
    {
        var imdata = InputMenuData(value, range, close_cb, return_cb);
        var input_menuitem = MenuItem(MenuItemINPUT, title, tip, 
            on_init_input, nil, nil);
        input_menuitem.arg = imdata;
        return input_menuitem;
    }

    ///////////////////////////////////
    //// File Selector
    ///////////////////////////////////


    function CreateSelectInFile(target_ref, path, title, tip, none_txt)
    {
        var candidates = fileToList(path, true);
        if (candidates == nil)
            candidates = [ ["none", "none"] ];
        return CreateSelect(target_ref, title, tip, candidates, none_txt);
    }

    class FEXMenu
    {
        attribute file = nil;
        attribute func = nil;
        attribute ends_with = nil;
        attribute write_mode = nil;

        method __init__(file, func, ends_with, write_mode)
        {
            self.file = file;
            self.func = func;
            if (ends_with == nil)
                ends_with = "";
            self.ends_with = ends_with;
            self.write_mode = write_mode;
        }
    }


    static DIRCOL = main::menu::MColorObject(0.05, 0.05, 0.1);
    static FILECOL = main::menu::MColorObject(0.05, 0.05, 0.05);
    static NEWCOL = main::menu::MColorObject(0.05, 0.1, 0.1);
    static TXTCOL = main::menu::MColorObject(0.05, 0.1, 0.1);
    static GPBCOL = main::menu::MColorObject(0.05, 0.1, 0.05);
    static IMGCOL = main::menu::MColorObject(0.1, 0.1, 0.05);

    function entry_sort_function(e1, e2)
    {
        return e1["name"] < e2["name"];
    }

    function on_fex_file(context, index, menuitem, item, fexmenu)
    {
        if (fexmenu.func != nil)
            fexmenu.func(context, index, menuitem, item, fexmenu);
        else
        {
            if (fexmenu.file.endsWith(".gpb"))
            {
                var gpb = main::pdistd::MGpb();
                gpb.loadHeaderOnly(fexmenu.file);
                var keylist = gpb.getKeyList();
                var menuitems = [];
                menuitems.push(GPBCOL);
                for (var i = 0; i < keylist.size; i++)
                {
                    var menuitem = MenuItem(MenuItemTEXT, keylist[i][0], "", nil, 
                    "%d bytes".format(keylist[i][1]), nil);
                    menuitems.push(menuitem);
                }
                SoundRoot.open_menu(context, fexmenu.file, menuitems, index, 0);
            } 
            else if (fexmenu.file.endsWith(".img")) 
            {
                ViewRoot.open(context, fexmenu.file);
            }
            else
            {
                // possibly go to
                var lines = fileToList(fexmenu.file);
                var menuitems = [];
                menuitems.push(TXTCOL);
                for (var i = 0; i < lines.size; i++)
                {
                    var menuitem = MenuItem(MenuItemTEXT, lines[i], "", nil, nil, nil);
                    menuitems.push(menuitem);
                }
                SoundRoot.open_menu(context, fexmenu.file, menuitems, index, 0);
            }
        }
    }

    function on_fex_dir(context, index, menuitem, item, fexmenu)
    {
        var entries = main::pdistd::GetFileEntries(fexmenu.file);

        if (entries == nil || entries.size == 0)
        {
            if (!fexmenu.write_mode)
            {
                DialogUtil::openConfirmDialog(context, DialogUtil::ERROR, "[{0}]を開けないか、\nファイルが存在していません。".build(fexmenu.file));
                return;
            }
        }
        else
        {
            entries.sort(entry_sort_function);
        }
        
        var menu = Array();

        if (fexmenu.write_mode)
        {
            var menuitem = MenuItem(MenuItemTEXT, "新規ファイル", "", nil, nil, on_fex_file);
            menuitem.arg = FEXMenu(fexmenu.file + "/", fexmenu.func, fexmenu.ends_with, fexmenu.write_mode);
            menuitem.base_color = NEWCOL;
            menu.push(menuitem);
        }

        if (entries != nil && entries.size != 0)
        {
            entries.each(function(entry)
            {
                var filename = entry["name"];
                if (entry["type"] == "DIRECTORY")
                {
                    var menuitem = MenuItem(MenuItemTEXT, filename, "", nil, nil, on_fex_dir);
                    menuitem.arg = FEXMenu(fexmenu.file + "/" + filename, fexmenu.func, fexmenu.ends_with, fexmenu.write_mode);
                    menuitem.base_color = DIRCOL;
                    menu.push(menuitem);
                }
                else if ((entry["type"] == "COMPRESSED" || entry["type"] == "REGULAR") 
                    && 
                    filename.endsWith(fexmenu.ends_with))
                {
                    var size = entry["size"];
                    var compressed_size = entry["compressed_size"];
                    var size_str;
                    if (compressed_size != 0 && compressed_size != size)
                        size_str = main::pdiext::GetNumberString(compressed_size) + "(" + main::pdiext::GetNumberString(size) + ") bytes";
                    else 
                        size_str = main::pdiext::GetNumberString(size) + " bytes";
                    
                    var menuitem = MenuItem(MenuItemTEXT, filename, "", nil, size_str, on_fex_file);
                    menuitem.arg = FEXMenu(fexmenu.file + "/" + filename, fexmenu.func, fexmenu.ends_with, fexmenu.write_mode);
                    menuitem.base_color = FILECOL;
                    menu.push(menuitem);
                }
            });
        }

        SoundRoot.open_menu(context, menuitem.label, menu, index, 0);
    }


    function CreateFileExplorer(dir, func, ends_with, title, tip, on_update)
    {
        var select_fex_menuitem = MenuItem(MenuItemTEXT, title, tip, nil, on_update, on_fex_dir);
        select_fex_menuitem.arg = FEXMenu(dir, func, ends_with, false);
        return select_fex_menuitem;
    }
    function CreateFileExplorerForWrite(dir, func, ends_with, title, tip, on_update)
    {
        var select_fex_menuitem = MenuItem(MenuItemTEXT, title, tip, nil, on_update, on_fex_dir);
        select_fex_menuitem.arg = FEXMenu(dir, func, ends_with, true);
        return select_fex_menuitem;
    }

    function on_update_information(context, index, menuitem, item)
    {
        var text = "nil";
        if (menuitem.arg != nil && menuitem.arg[1] != nil)
            text = menuitem.arg[1];
        item["value"].text = text;
    }

    function on_activate(context, index, menuitem, item, arg)
    {
        var activate_func = arg[0];
        var activate_arg = arg[2];
        activate_func(context, activate_arg);
    }

    function on_activate_information(context, index, menuitem, item, arg)
    {
        var display_list_func = arg[0];
        if (display_list_func == nil)
            return;

        var display_list_arg = arg[1];
        var displaylist = display_list_func(context, display_list_arg);

        if (displaylist != nil)
        {
            var menu = [];
            foreach (var cols in displaylist)
            {
                if (cols.isInstanceOf(main::menu::MColorObject))
                {
                    menu.push(cols);
                    continue;
                }

                var title = cols[0];
                var text = cols[1];
                var tip = "";
                if (cols.size >= 3)
                    tip = cols[2];
                var next_display_list_func = nil;
                if (cols.size >= 4)
                    next_display_list_func = cols[3];

                var on_activate_func = nil;
                if (cols.size >= 5)
                    on_activate_func = cols[4];
                var on_activate_arg = nil;
                if (cols.size >= 6)
                    on_activate_arg = cols[5];

                if (on_activate_func != nil)
                {
                    var mitem = MenuItem(MenuItemTEXT, title, tip, nil, 
                        on_update_information, on_activate);
                    mitem.arg = [on_activate_func, text, on_activate_arg];
                    menu.push(mitem);
                }
                else 
                {
                    var mitem = MenuItem(MenuItemTEXT, title, tip, nil, 
                        on_update_information, on_activate_information);
                    mitem.arg = [next_display_list_func, text];
                    menu.push(mitem);
                }
            }

            SoundRoot.open_menu(context, menuitem.label, menu, index, 0);
        }
    }

    function CreateInformation(display_list_func, title, tip, arg)
    {
        var menuitem = MenuItem(MenuItemTEXT, title, tip, nil, nil, on_activate_information);
        menuitem.arg = [display_list_func, arg];
        return menuitem;
    }
}
