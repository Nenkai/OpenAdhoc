











module SideSelectRoot
{
    hidden.visible = false;

    attribute sModalPage;
    attribute sData;
    attribute sResults;
    attribute sActivatedNum;
    attribute sConditionList;
    attribute sOrgPaneX;

    class Data
    {
        attribute title_;
        attribute translate_page_;
        attribute extra_translate_page_;
        attribute candidates_;
        attribute enable_multi_;
        attribute select_max_;
        attribute initial_selected_indices_;
        attribute cb_init_item_;
        attribute cb_focus_enter_;
        attribute disable_cancel_;
        attribute window_x_;
        attribute frame_red_bring_;

        method __init__()
        {
            enable_multi_ = false;
            select_max_ = 1;
            disable_cancel_ = false;
            window_x_ = -1;
            frame_red_bring_ = true;
        }
    }

    method onLoad(context)
    {
        
    }

    method open(context, data)
    {
        self.sData = data;
        if (data.window_x_ >= 0)
            Pane.x = data.window_x_;

        Pane::SBox::FrameImage.visible = data.frame_red_bring_;
        self.sConditionList = Array(data.candidates_.size);

        for (var i = 0; i < self.sConditionList.size; i++)
            self.sConditionList[i] = false;

        if (data.enable_multi_)
        {
            if (data.initial_selected_indices_ != nil)
            {
                for (var i = 0; i < data.initial_selected_indices_.size; i++)
                {
                    self.sConditionList[data.initial_selected_indices_[i]] = true;
                }

                self.sActivatedNum = data.initial_selected_indices_.size;
            }
            else
                self.sActivatedNum = 0;
        }

        appearAction();

        self.sModalPage = SequenceUtil::ModalPage(context, SideSelectRoot);
        self.sModalPage.open();

        closeAction(context);

        var results = self.sResults;
        self.sResults = nil;
        self.sConditionList = nil;
        return results;
    }

    method close(context, selected_list)
    {
        if (self.sModalPage != nil)
        {
            closeAction(context);
            self.sModalPage.close();
            self.sModalPage = nil;
            self.sResults = selected_list;
        }
    }

    method abort(context)
    {
        if (self.sModalPage != nil)
            self.close(context, nil);
    }

    method initialize_listbox(context)
    {
        var candidates = SideSelectRoot.sData.candidates_;
        var enable_multi = SideSelectRoot.sData.enable_multi_;

        hidden::Item::Slit.visible = enable_multi;
        
        var listbox = Pane::SBox::ListBox;
        listbox.setItemCount(candidates.size);
        listbox.setItemTemplate(context, hidden::Item);

        if (!enable_multi)
        {
            var indices = SideSelectRoot.sData.initial_selected_indices_;
            if (indices != nil && indices.size > 0)
                listbox.focus_index = indices[0];
        }

        Pane::Footer.visible = !enable_multi;
    }

    method resolve_window_y(context)
    {
        var candidates = SideSelectRoot.sData.candidates_;
        var item_num = candidates.size;
        var item_h = hidden::Item.h;
        if (item_num > 11)
            item_num = 11;

        var item_total_h = item_num * item_h;
        Pane::SBox::ListBox.h = item_total_h;
    }

    method onInitialize(context)
    {
        var data = self.sData;
        if (data.title_ != nil)
        {
            if (data.extra_translate_page_ != nil)
            {
                Pane::Title::label.text = context.translate(SideSelectRoot, data.extra_translate_page_, data.title_);
            }
            else
            {
                Pane::Title::label.text = context.translate(data.translate_page_, data.title_);
            }
        }

        initialize_listbox(context);
        resolve_window_y(context);
        SideSelectRoot.setFocus(Pane::SBox::ListBox);
    }

    method onFinalize(context)
    {
        var listbox = Pane::SBox::ListBox;
        listbox.setItemCount(0);
    }

    method onCancel(context)
    {
        if (self.sData.disable_cancel_)
            return STATE_YIELD;

        main::sound.play("cancel");
        self.close(context, nil);
        return STATE_YIELD;
    }

    method isActivate(index)
    {
        return self.sConditionList[index];
    }

    method activate(index)
    {
        var item = Pane::SBox::ListBox.getItemWidget(index);
        if (item != nil)
            item.Slit.active = true;

        self.sConditionList[index] = true;
    }

    method deactivate(index)
    {
        var item = Pane::SBox::ListBox.getItemWidget(index);
        if (item != nil)
            item.Slit.active = false;

        self.sConditionList[index] = false;
    }

    method hide_action(widget, is_close)
    {
        widget.ScalarInterpolator.reverse = is_close;
        if (!is_close)
            widget.ScalarInterpolator.restart();
        else
            widget.ScalarInterpolator.start();
    }

    method appearAction()
    {
        hide_action(BaseBG, false);
        hide_action(Pane, false);
        hide_action(ToolTip, false);
    }

    method closeAction(context)
    {
        hide_action(BaseBG, true);
        hide_action(Pane, true);
        hide_action(ToolTip, true);

        context.sync(BaseBG.ScalarInterpolator.total_time);
    }

    module Pane::SBox::ListBox
    {
        method onVisibleNotify(context, indexes)
        {
            foreach (var i in indexes)
            {
                if (self.getItemVisible(i))
                {
                    var item = self.getItemWidget(i);
                    if (item == nil)
                    {
                        item = self.getItemWidget(i, context);
                        item.initialize(context, SideSelectRoot.sData.candidates_[i], SideSelectRoot.sConditionList[i]);
                    }
                }
            }
        }

        method onFocusNotify(context, new_index, old_index)
        {
            if (SideSelectRoot.sData.cb_focus_enter_ != nil)
                SideSelectRoot.sData.cb_focus_enter_(context, self, new_index);

            return 1;
        }

        method onActivate(context)
        {
            var fidx = self.focus_index;
            var focus_item = self.getItemWidget(fidx, context);
            var enable_multi = SideSelectRoot.sData.enable_multi_;

            if (enable_multi)
            {
                if (SideSelectRoot.isActivate(fidx))
                {
                    main::sound.play("cancel");
                    SideSelectRoot.deactivate(fidx);
                    SideSelectRoot.sActivatedNum--;
                }
                else if (SideSelectRoot.sActivatedNum >= SideSelectRoot.sData.select_max_)
                {
                    main::sound.play("disable");
                }
                else
                {
                    main::sound.play("ok");
                    SideSelectRoot.activate(fidx);
                    SideSelectRoot.sActivatedNum++;
                }
            }
            else
            {
                main::sound.play("ok");
                var candidates = SideSelectRoot.sData.candidates_;
                SideSelectRoot.close(context, [fidx]);
            }

            return STATE_YIELD;
        }

        method onKeyPress(context, event)
        {
            switch (event.keysym)
            {
                case CELL_PAD_CTRL_L3_LEFT:
                    self.decIndex(5);
                    return STATE_YIELD;

                case CELL_PAD_CTRL_L3_RIGHT:
                    self.incIndex(5);
                    return STATE_YIELD;
            }

            return STATE_EXIT;
        }
    }

    module hidden::Item
    {
        method initialize(context, candidate, active)
        {
            if (SideSelectRoot.sData.cb_init_item_ != nil)
                SideSelectRoot.sData.cb_init_item_(context, self, candidate);
            else
                self.label.text = candidate.toString();

            Slit.active = active;
        }
    }
}