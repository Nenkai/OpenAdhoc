module LaboModeProject::SaveReplayRoot
{

	static bestlapmode = 0;
	
	function onInitialize(context)
	{
		SaveReplayRoot.visible = 1;
	
		enterPage(context, SaveReplayRoot, hidden::Title::title.doCopy());
	
		var newitem = hidden::NewItem.doCopy();
		newitem.x = 0;
		newitem.y = 0;
	
		SaveReplayRoot["Common"]["AppendBG"].appendChild(context, newitem);
	
		selectFolder_ = 0;
		hidden.visible = false;
	
		hideReplayItem(SaveReplayRoot);
	
		hidden::ReplayItem::label.text = "";
	
		init(context);
	
		SaveReplayRoot["Common"]["Command"]["DeleteMode"].visible = 1;
	
		Template::setExitActivate(commandExit);
		Template::setOptionActivate(commandOption);
	}
	
	function onFinalize(context)
	{
		leavePage(context, SaveReplayRoot);
	}
	
	function onCancel(context)
	{
		main::sound.play("cancel");
		exit(context);
		return 2;
	}
	
	function exit(context)
	{
		start_page(context, nil);
	}
	
	function init(context)
	{
		context.cursor_visible = 1;
	
		initCommon(context, SaveReplayRoot);
		initFolders(context);
		initCommands(context);
		initList(context);
	
		setDragable(0);
		setAliasFileMode(0);
		setHasNewFile(1);
	
		manager = nil;
	
		var slot = currentSlot();
		var mode = main::game.next_menu_arg;
	
		//print("mode == " + mode);
	
		manager = main::menu::MMemoryCardManager(mode, slot);
	
		bestlapmode = (mode == "MODE_SAVE_BESTLAP_REPLAY");
	
		var file = getFile(0);
		var racemode = file.racemode;
		var submode = file.racesubmode;
	
		//print("mode = " + racemode + ", " + submode);
	
		var modestr = context.translate(SaveReplayRoot, "RaceModeName", racemode);
	
		if (submode >= 0)
			modestr += submode.toString();
	
		file.title = modestr;
	
		var newItem = hidden::ReplayItem.doCopy();
		newItem.can_focus = 0;
	
		setupWidgetReplay(context, 0, newItem);
	
		var saveItem = SaveReplayRoot["Common"]["AppendBG"]["NewItem"]["SaveItem"];
		saveItem.appendChild(context, newItem);
	
		SaveReplayRoot.setFocus(getList());
	
		manager.start();
	}
	
	
	function currentSlot()
	{
		return main::game.option.replay_memory_card_slot;
	}
	
	static folders_ = [];
	
	function initFolders(context)
	{
		initFolderCommon(context, folders_);
	}
	
	function commandDummy(context)
	{
		print("dummy");
	}
	
	function commandOption(context)
	{
		print("option!");
	
		var slot = currentSlot();
	
		ReplaySlotDialog::open(context);
	
		if (slot != currentSlot())
		{
			init(context);
		}
	}
	
	function commandExit(context)
	{
		print("exit");
		exit(context);
	}
	
	function initCommands(context)
	{
	}
	
	function initList(context)
	{
		initListCommon(context, hidden::ReplayItem);
	}
	
	function setupWidget(context, i, w)
	{
		w["label"].text = context.translate(
			SaveReplayRoot,
			(i == 0) ? "create new file" : "overwrite"
		);
	
		if (i == 0)
			return 1;
	
		setupWidgetReplay(context, i, w);
	}
}

module LaboModeProject::SaveReplayRoot::Scroll::List
{
	function onActivate(context, event)
	{
		var focus_index = getList().focus_index;
	
		if (focus_index < 0 || getFileCount() < focus_index)
			return 1;
	
		var newfile = manager.getNewFile();
		var file = getFile(focus_index);
	
		if (file == nil || newfile == nil)
			return 1;
	
		var f_title = file.title;
	
		manager.requestPause();
		main::sound.play("ok");
	
		print("openEntry!");
	
		SaveReplayRoot.visible = false;
	
		if (EntryRoot::open(context, 1, newfile.title))
		{
			manager.waitPause();
			SaveReplayRoot.visible = true;
	
			newfile.title = EntryRoot::getText();
	
			var r = saveVariousDataForSelector(
				context,
				manager,
				bestlapmode ? "best_replay" : "full_replay",
				currentSlot(),
				file,
				newfile
			);
	
			if (r != 0)
			{
				clearList(context);
				manager.clear();
			}
		}
	
		print("closeEntry!");
	
		SaveReplayRoot.visible = true;
		manager.restart();
		return 1;
	}
	
	function onKeyPress(context, event, item)
	{
		LaboModeProject::onListKeyPress(context, event);
		return;
	}
	
	function onKeyRelease(context, event)
	{
		LaboModeProject::onListKeyRelease(context, event);
		return 0;
	}
}